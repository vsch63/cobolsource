000010*Property of FIS©. or its affiliates
000020*all rights reserved. FIS Confidential
000030 IDENTIFICATION DIVISION.
000040 PROGRAM-ID.           CACBLTUB.
000050 AUTHOR.               DANIEL WILLIAMS
000060 DATE-WRITTEN.         03-18-1997.
000070****************************************************************
000080*   $Modtime::   10 Oct 2018 10:15:16  $
000090*   $Log::   P:/pvcsproj/DONOTUSE.prj/source/CACBLTUB.COV  $
000100*
000110*   Rev 1.0.1.32   10 Oct 2018 11:45:24   alex
000120*FDM4167
000130*fis copyright
000140*
000150*   Rev 1.0.1.31   19 Apr 2011 17:27:26   tom
000160*LSM5259
000170*Replace call to CUTDTCAL
000180*
000190*   Rev 1.0.1.30   28 Apr 2006 14:19:38   katrina
000200*LPM0066
000210*Contract Number Size Change
000220*
000230*   Rev 1.0.1.29   23 Sep 2005 11:41:20   robin
000240*FDM3514
000250*Module Impact Analysis Tool - WS-REVISION awk script fix.
000260*
000270*   Rev 1.0.1.28   19 Sep 2005 18:58:18   robin
000280*FDM3514
000290*Module Impact Analysis Tool
000300*
000310*   Rev 1.0.1.27   22 Nov 2004 18:27:34   gabriela
000320*FLM0001
000330*scheme level currency
000340*
000350*   Rev 1.0.1.26   20 Dec 2002 10:56:08   rieko
000360*FDM3175
000370*COBOL modifications to support the Oracle 8 pre-complier.
000380*
000390*   Rev 1.0.1.25   09 Jun 2000 14:53:30   barry
000400*FDM2160
000410*Expand CASE-KEY
000420*
000430*   Rev 1.0.1.24   Mar 24 2000 17:42:46   miker
000440*FDM1959
000450*Expanded Keys project
000460*
000470*   Rev 1.0.1.23   Aug 10 17:42:48 1999   richardf
000480*FDM1848
000490*
000500*   Rev 1.0.1.22   Apr 30 14:38:12 1999   moin
000510*FDM1511
000520*No DataBase Changes
000530*
000540*   Rev 1.0.1.21   Feb 23 09:18:30 1999   dave
000550*FDM1332
000560*Populate SCAT.Bundle_Id before Insert
*******
*******   Rev 1.0.1.20   Dec 18 16:58:06 1997   dan
*******afz0314
*******
*******   Rev 1.0.1.19   Dec 12 15:32:10 1997   dan
*******afa0314
*******
*******   Rev 1.0.1.18   02 Dec 1997 14:34:52   dan
*******AFA0314
*******
*******   Rev 1.0.1.17   13 Nov 1997 11:25:52   dan
*******afa0314
*******
*******   Rev 1.0.1.16   02 Sep 1997 10:31:02   don
*******fdm0840
*******for Dan
*******
*******   Rev 1.0.1.16   02 Sep 1997 09:59:50   don
*******fdm0840
*******for Dan
*******
*******   Rev 1.0.1.15   10 Jul 1997 12:20:10   paulm
*******Mass Check In for Currency Field Changes
*******
*******   Rev 1.0.1.14   11 Jun 1997 15:11:44   dan
*******AFA0142
*******
*******   Rev 1.0.1.13   11 Jun 1997 10:52:56   paulm
*******AFA0142
*******
*******   Rev 1.0.1.12   10 Jun 1997 09:19:16   dan
*******Fixed Reporting Module
*******
*******   Rev 1.0.1.11   29 May 1997 17:24:56   dan
*******AFM0101
*******
*******   Rev 1.0.1.10   29 May 1997 11:17:12   dan
*******AFM0101 FINISHED
*******
*******   Rev 1.0.1.9   23 May 1997 16:34:14   dan
*******AFM0101
*******
*******   Rev 1.0.1.8   22 May 1997 12:28:14   dan
*******AFM0101
*******
*******   Rev 1.0.1.7   21 May 1997 12:04:52   dan
*******AFM0101
*******
*******   Rev 1.0.1.6   20 May 1997 14:39:22   dan
*******AFM0101
001070*
001080*   Rev 1.0.1.5   19 May 1997 12:05:16   dan
001090*AFM0105  Check in before report logic,
001100*batch reporting logic is ok.
001110*
001120*   Rev 1.0.1.4   09 May 1997 14:46:16   dan
001130*
001140*
001150*   Rev 1.0.1.3   08 May 1997 10:04:48   dan
001160*
001170*
001180*   Rev 1.0.1.2   07 May 1997 15:43:12   dan
001190*
001200*
001210*   Rev 1.0.1.1   06 May 1997 16:27:10   dan
001220*
001230*
001240*   Rev 1.0.1.0   May 03 21:10:06 1997   paulm
001250*No ERF
001260*
001270*Initial Check In for FDP Branch
001280*3 May 1997
001290*
001300*   Rev 1.0   03 May 1997 14:28:50   paulm
001310*No ERF
001320*
001330*Initial Check-In   3 May 1997
001340****************************************************************
001350*
001360* MONTH END ACCOUNT BALANCING.  FOUR LEVELS OF BALANCING ARE
001370* SUPPORTED IN THIS PROGRAM
001380****************************************************************
001390* THIS PROGRAM CALCULATES BEGINNING AND ENDING ACCOUNT BALANCES.
001400* FOUR LEVELS OF BALANCING ARE AVAILABLE LEDGER, SYSTEM, PRODUCT
001410* AND CASE.  THE PROGRAM WILL UPDATE/INSERT RECORDS INTO THE COMP FLM0001
001420* ACCOUNTS TABLE.  IT ALSO UPDATES LAST ACCOUNT BALANCE DATE ON   FLM0001
001430* SYSTEM_DATA.  THIS PROGRAM WILL BE RUN ON A MONTHLY BASIS IN
001440* BATCH.  THIS IS A STEP PROGRAM.                                 FLM0001
001450*
001460****************************************************************
001470*    THE FOLLOWING ERROR CODES MAY BE RETURNED IN RTN-ERROR-CODE
001480*    FROM THIS PROGRAM...
001490*
001500*
001510****************************************************************
001520*                     CHANGES LOG
001530****************************************************************
001540*
001550*   DATE     PRG   PROJ#      DESCRIPTION
001560*  --------  --- ---------  ---------------------------------------
001570* 1997-04-26 DEW AFI0069    INSERT A RECORD INTO SCHED_ACCT_BAL
001580*                           FOR REVERSAL JOB
001590* 1997-05-02 DEW AFM0100    BATCH REPORTING
001600* 1997-05-19 DEW AFM0101    ADDED REPORT LOGIC,  FDPPRINT WILL
001610*                           PROCESS PRINTDATA PRODUCED BY THIS
001620*                           PROGRAM.
003700* 1997-05-29 DEW AFM0101    CHANGED BAL_AMT FIELDS TO S9(13)V99
003700* 1997-06-11 DEW AFA0142    REPAIR THE REPORT LOGIC TO PUT OUT
001650*                           LEDG,COMP,PROD,SCHE RECORD TYPES
003800*                           ADDED WS-RPTREQUEST FLAG FOR
003800*                           ROLLBACK LOGIC
003800*                           CHANGED SORTDATA ORDER FOR REPORT
003700* 08-11-1997 DEW FDM0840    EDIT THE LAST ACCT BAL DT, IT MUST
003800*                           BE A MONTH END DT, EDIT IS IN THE DAL
003800*                           ADDED COMMA AS FIRST CHAR OF DETL-
003800*                           REC2.  CREATE SCHED ACTIVITY RECORD
003800*                           IN THE A000 PARAGRAPH
003700* 11-13-1997 DEW AFA0314    MODIFICATIONS FOR HARMONY WITH NEW
003700*                           ONLINE BALANCING PROGRAM
001760* 19-FEB-99  DSR  FDM1332   ADD BUNDLE-ID TO SCAT1-DATA
001770* 30-APR-99  MOS  FDM1511   ADDED BEN-CAT-NO
001780* 10-AUG-99  RCF  FDM1848   INITIALIZE RPRQ1-DATA BEFORE CALLING
001790*                           CI1RPRQD
001800* 24-MAR-00  MR   FDM1959   Expanded Keys project
001810* 31-MAY-00  BST  FDM2160   Expand CASE-KEY
001820* 17-NOV-04  GVC  FLM0001   ADD EDIT FOR NEW SCHEME CURRENCY CD
001830*
001900******************************************************************
002000* THIS DRIVER MODULE CALLS 31 PROGRAMS.  THEY ARE LISTED BELOW.
002100*
002200* PROGRAM  LINKAGE  PARA NOTE
002300* -------- -------- ---- -----------------------------------------
002400* CCOMMITD N/A      A000 RTN-ERROR COMMIT RECORDS
002500* CS1SYSDD CL1SYSDD A100 READ SYSTEM DATA
002500* CS2SYSDD CL1SYSDD A100 VALIDATE LAST ACCT BAL DATE
002600* CUTDTCAL CWSDTCAL A100 CALC NEXT MONTH END DATE
002700* CF1COACD CL2COACD A200 SELECT ALL COMPANY ACCTS FROM LAST MONTH
002700* CI3COACD CL1COACD A210 INSERT COMPANY ACCTS FOR THIS RUN
002800* CS1VRPDD CL1VRPDD A300 SELECT REPORT REF ID
002800* CI1RPRQD CL1RPRQD A400 INSERT INTO REPORT REQUEST
002500* CI1PRDTD CL1PRDTD A500 CREATE REPORT HEADING IN PRINTDATA
002800* CF1VJLLD CL4JLOGD B000 BLOCK FETCH JOURNAL LOGS
002800* CU1SYSDD CL1SYSDD B000 ADVANCE SYSTEM DATA DATE ONE MONTH
002800* CS2ACCTD CL1ACCTD B100 SINGLE SELECT LEDGER ACCOUNT
002800* CD1COACD CL1COACD B150 DELETE COACS AT WRONG BAL. LEVEL
002800* CU2COACD CU2COACD B720 UPDATE ENDING BALANCE AMOUNT ON COAC
002800* CS2JLOGD CL3JLOGD B740 SUMMARIZE JOURNAL LOGS
002800* CI1COACD CL1COACD B760 INSERT A COMPANY ACCOUNT
002800* CI1SCABD CL1SCABD B800 INSERT A SCHED_ACCT_BAL RECORD FOR JOB
002800* CI1PRDTD CL1PRDTD B930 INSERT A PRINTDATA RECORD
002800* CS2COACD CL1COACD B940 SELECT COAC FOR BAL AMOUNTS FOR RPT
002800* CS2SCATD CL1SCATD Z000 BATCH REPORTING
002800* CU3SCATD CL1SCATD Z000 BATCH REPORTING
002800* CS1TREFD N/A      Z100 BATCH REPORTING GENERATE A TR-REF-NO
002800* CI1SCATD CL1SCATD Z100 BATCH REPORTING INSERT SCHED_ACTIVITY
002800* CU1SCATD CL1SCATD Z200 BATCH REPORTING SET SCHED_ACT STATUS
002800* CD2COACD CL1COACD Z300 RTN-ERROR DELETE ALL CREATED COACS
002800* CD1PRDTD CL1PRDTD Z320 RTN-ERROR DELETE THE PRINTDATA
002800* CD1RPRQD CL1RPRQD Z340 RTN-ERROR DELETE THE REPORT REQUEST
002800* CU4SCATD CL1SCATD Z400 RTN-ERROR SET ALL SCHED_ACT=UNPROCESSED
002800* CS1RFCDD CL1RFCDD Z600 GET COMPANY DESCRIPTION FOR REPORT
002800* CS1RFCDD CL1RFCDD Z700 GET PRODUCT DESCRIPTION FOR REPORT
002800* CS5CASDD CL3CASDD Z800 GET SCHEME INFORMATION FOR REPORT
003000* CUTSVARB          XXXX INSERT A VARIANCE
003100*
003200******************************************************************
002230 ENVIRONMENT DIVISION.
002240 CONFIGURATION SECTION.
002250*
002260 DATA DIVISION.
002270 WORKING-STORAGE SECTION.
002280
002290 01  WS-PGM-NAME                          PIC X(8)
002300                                          VALUE 'CACBLTUB'.
002310
002320 01  WS-COMPILED                          PIC X(20).              FDM1332
002330
002340 01  WS-SUBSCRIPTS.
002350     05  IJLOG                            PIC S9(4) COMP.
002360     05  ICOAC                            PIC S9(4) COMP.
002370     05  IBARL                            PIC S9(4) COMP.
002380
002390 01  WS-CODES.
002400     05  WS-BALANCE-LEVEL                 PIC X(02) VALUE SPACES.
002410         88 NOT-APPLICABLE                VALUE '00'.
002420         88 LEDGER-LEVEL                  VALUE '01'.
002430         88 COMPANY-LEVEL                 VALUE '02'.
002440         88 PRODUCT-LEVEL                 VALUE '03'.
002450         88 CASE-LEVEL                    VALUE '04'.
002460     05  WS-WARNING-FLAG                  PIC 9(01) VALUE ZERO.
002470         88 WARNING-NOT-ISSUED            VALUE 0.
002480     05  WS-PROCESS-FLAG                  PIC 9(01) VALUE ZERO.
006400         88 STARTUP-PROCESSING            VALUE 1.
006400         88 MAIN-PROCESSING               VALUE 2.
002510
002520 01  WS-HOLD-VARIABLES.
002530     05  WS-HOLD-ACCT-KEY                 PIC S9(15) COMP-3.      FDM1959
002540     05  WS-HOLD-CO-NUM                   PIC S9(05) COMP-3.
002550     05  WS-HOLD-PROD-NUM                 PIC S9(05) COMP-3.
002560*    05  WS-HOLD-CASE-KEY                 PIC S9(05) COMP-3.      FDM2160
002570     05  WS-HOLD-CASE-KEY                 PIC S9(15) COMP-3.      FDM2160
002580
002590 01  WS-HOLD-VARIABLES2.
002600*    THESE ARE FOR BATCH REPORTING   ACCT-NO TRUNCATED
002610     05  WS-HOLD-ACCT-NO                  PIC X(20).
002620     05  WS-HOLD-SUB-ACCT-NO              PIC X(12).
002630
002640 01  WS-ACCUMULATORS.
002650     05  WS-TOTAL-DEBITS                  PIC S9(13)V99 COMP-3.
002660     05  WS-TOTAL-CREDITS                 PIC S9(13)V99 COMP-3.
002670     05  WS-DB-CR-TOTAL                   PIC S9(13)V99 COMP-3.
002680
002690 01  WS-MISC-VARIABLES.
002700     05  WS-BEG-ACCT-BAL-DT               PIC X(10).
002710     05  WS-END-ACCT-BAL-DT.
002720         10  WS-ISO-YYYY2                 PIC 9999.
002730         10  FILLER                       PIC X.
002740         10  WS-ISO-MM2                   PIC 99.
002750         10  FILLER                       PIC X.
002760         10  WS-ISO-DD2                   PIC 99.
002770     05  WS-YRS-DIFF                      PIC S9(4)V9(5) COMP-3.
002780
002790     05  WS-BJLG-KEY                      PIC S9(15) COMP-3.      FDM1959
002800*    05  WS-SCAT-KEY                      PIC S9(08) COMP-3.      FDM1959
002810     05  WS-SCAT-KEY                      PIC S9(15) COMP-3.      FDM1959
002820     05  WS-ORIG-FDP-SCAT-KEY             PIC S9(15) COMP-3.      FDM1959
002830     05  WS-ORIG-FDP-TR-REF-NO            PIC X(12).
002840     05  WS-NEW-FDP-TR-REF-NO             PIC X(12).
002850     05  WS-RECTYPE                       PIC X(04).
002860     05  WS-RPTREQUEST                    PIC 9(01) VALUE 0.
002870*    FOR REPORT
002880     05  WS-SEQ-NUM                       PIC 9(08) VALUE 0.
002890*    FOR REF CODE LOOKUP
002900     05  WS-REF-NUM                       PIC ZZZZ9.
002910
002920 01  WS-ERROR-MESSAGES.
002930     05  WS-ERR-MSG1.
002940         10  FILLER                       PIC X(20)
002950             VALUE 'LEDGER ACCOUNT KEY: '.
002960*        10  WS-ERR-ACCT-KEY              PIC 9(08).              FDM1959
002970         10  WS-ERR-ACCT-KEY              PIC  9(15).             FLM0001
002980     05  WS-ERR-MSG2.
002990         10  FILLER                       PIC X(20)
003000             VALUE 'LEDGER ACCOUNT NO: '.
003010         10  WS-ERR-ACCT-NO               PIC X(20).
003020     05  WS-ERR-MSG3.
003030         10  FILLER                       PIC X(20)
003040             VALUE 'SUB ACCOUNT NO: '.
003050         10  WS-ERR-SUB-ACCT-NO           PIC X(12).
003060* BARL ERROR DETAILS
003070     05  WS-ERR-MSG4.
003080         10  FILLER                       PIC X(15)
009500             VALUE 'BARL KEY:    '.
003100*        10  WS-ERR-BARL-KEY              PIC 9(08).              FDM1959
003110         10  WS-ERR-BARL-KEY              PIC  9(15).             FLM0001
003120     05  WS-ERR-MSG5.
003130         10  FILLER                       PIC X(15)
009500             VALUE 'DESCRIPTION: '.
003150         10  WS-ERR-DESCRIPT              PIC X(25).
003160     05  WS-ERR-MSG6.
003170         10  FILLER                       PIC X(20)
003180             VALUE 'NEXT BAL DATE: '.
003190         10  WS-ERR-NXT-BAL-DT            PIC X(10).
003200     05  WS-ERR-MSG7.                                             FLM0001
003210         10  FILLER                       PIC X(15)               FLM0001
003220             VALUE 'BEG BAL DATE: '.                              FLM0001
003230         10  WS-ERR-BEG-BAL-DT            PIC X(10).              FLM0001
003240
003250     05  WS-ERR-MSG8.                                             FLM0001
003260         10  FILLER                       PIC X(15)               FLM0001
003270             VALUE 'END BAL DATE: '.                              FLM0001
003280         10  WS-ERR-END-BAL-DT            PIC X(10).              FLM0001
003290
003300* WS FOR THE REPORT
003310 01  CHR34-NUM PIC 9(4) BINARY VALUE 34.
003320 01  CHR34-NUM-REDEF REDEFINES CHR34-NUM.
003330     05  FILLER PIC X.
003340     05  CHR34 PIC X.
003350
003360**   PUTS IN A DOUBLE QUOTE
003370 01  HEAD-REC.
003380     05  FILLER1                          PIC X(01).
003390     05  HEAD-LAST-ACCT-BAL-DT            PIC X(10).
003400     05  FILLER                           PIC X(03) VALUE '","'.
003410     05  HEAD-NEW-ACCT-BAL-DT             PIC X(10).
003420     05  FILLER                           PIC X(03) VALUE '","'.
003430     05  HEAD-CYC-DT                      PIC X(10).
003440     05  FILLER2                          PIC X(01).
003450
003460 01  DETL-REC.
003470     05  FILLER3                          PIC X(01).
003480     05  DETL-ACCT-NO                     PIC X(12).
003490     05  FILLER                           PIC X(03) VALUE '","'.
003500     05  DETL-SUB-ACCT-NO                 PIC X(12).
003510     05  FILLER                           PIC X(03) VALUE '","'.
003520     05  DETL-ACCT-NM                     PIC X(30).
003530     05  FILLER                           PIC X(03) VALUE '","'.
003540* TRUNCATED FROM 80 TO 50
003550     05  DETL-COMPANY-DESCRIPT            PIC X(50).
003560     05  FILLER                           PIC X(03) VALUE '","'.
003570* TRUNCATED FROM 80 TO 50
003580     05  DETL-PRODUCT-DESCRIPT            PIC X(50).
003590     05  FILLER                           PIC X(03) VALUE '","'.
003600     05  DETL-BAL-LVL-CD                  PIC X(02).
003610     05  FILLER                           PIC X(03) VALUE '","'.
003620     05  DETL-CONT-NO                     PIC X(15).              LPM0066
003630     05  FILLER                           PIC X(03) VALUE '","'.
003640     05  DETL-CAT-NO                      PIC X(10).
003650     05  FILLER                           PIC X(03) VALUE '","'.
003660* TRUNCATED FROM 80 TO 50
003670     05  DETL-PLAN-NM                     PIC X(50).
003680     05  FILLER4                          PIC X(01).
003690
003700 01  DETL-REC2.
003710     05  FILLER                           PIC X(01) VALUE ",".
003720     05  FILLER5                          PIC X(01).
003730     05  DETL2-BEG-BAL-AMT                PIC -ZZZZZZZZZZZZ9.99.
003740     05  FILLER                           PIC X(03) VALUE '","'.
003750     05  DETL2-TOT-CREDITS                PIC -ZZZZZZZZZZZZ9.99.
003760     05  FILLER                           PIC X(03) VALUE '","'.
003770     05  DETL2-TOT-DEBITS                 PIC -ZZZZZZZZZZZZ9.99.
003780     05  FILLER                           PIC X(03) VALUE '","'.
003790     05  DETL2-END-BAL-AMT                PIC -ZZZZZZZZZZZZ9.99.
003800     05  FILLER                           PIC X(03) VALUE '","'.
006936     05  DETL2-TR-REF-NO                  PIC X(12).
003820     05  FILLER6                          PIC X(01).
003830
003840 01  SORT-REC.
003850     05  SORT-BAL-LVL-CD                  PIC X(02).
003860* TRUNCATED FROM 80 TO 50
003870     05  SORT-COMPANY-DESCRIPT            PIC X(50).
003880* TRUNCATED FROM 80 TO 50
003890     05  SORT-PRODUCT-DESCRIPT            PIC X(50).
003900     05  SORT-CONT-NO                     PIC X(15).              LPM0066
003910     05  SORT-CAT-NO                      PIC X(10).
003920     05  SORT-ACCT-NO                     PIC X(12).
003930     05  SORT-SUB-ACCT-NO                 PIC X(12).
003940
003950*    SYSTEM DATA COPYBOOK
003960     COPY CL1SYSDD.
003970*    LEDGER ACCOUNT COPYBOOK
003980     COPY CL1ACCTD.
003990*    COMPANY ACCOUNTS COPYBOOK
004000     COPY CL1COACD.
004010*    COMPANY ACCOUNTS COPYBOOK BLOCK FETCH
004020     COPY CL2COACD.
004030**   JOURNAL_LOGS                                                 FLM0001
004040     COPY CL1JLOGD.                                               FLM0001
004050*    JOURNAL LOG COPYBOOK BLOCK FETCH
004060     COPY CL4JLOGD.
004070*    JOURNAL LOG COPYBOOK SUMMARY
004080     COPY CL3JLOGD.
004090*    JOURNAL LOG COPYBOOK SUMMARY
004100     COPY CL5JLOGD.
004110*    DATE CALCULATION COPYBOOK
004120     COPY CWSDTCAL.
004130*    SCHED_ACCT_BAL COPYBOOK
004140     COPY CL1SCABD.
004150*    SCHED_ACTIVITY COPYBOOK
004160     COPY CL1SCATD.
004170**   COPYBOOK FOR RPTDEF,RPTVERS,RPTREFLNK JOIN
004180     COPY CL1VRPDD.
004190**   COPYBOOK FOR RPTREQUEST
004200     COPY CL1RPRQD.
004210*    PRINTDATA COPYBOOK
004220     COPY CL1PRDTD.
004230*    REF_CODES COPYBOOK
004240     COPY CL1RFCDD.
004250*    CASE_DATA COPYBOOK
007312     COPY CL3CASDD.
004270*    BAL_ACCT_RULES
007312     COPY CL2BARLD.
004290
004300**  REVISION AREA **                                              FDM3514
004310 01  WS-REVISION           PIC X(35)  VALUE                       FDM3514
004320     ' $Revision::   1.0.1.32       $'.                           
004330 LINKAGE SECTION.
004340*    RMESSAGE AREA
004350     COPY CLKFDPNC.
004360*    CASE  DATA COPYBOOK
004370     COPY CL1CASDD.
004380*    STANDARD ERROR BLOCK
004390     COPY CLKERRBC.
004400
004410*********************************************************
004420**   P R O C E D U R E    D I V I S I O N              **
004430*********************************************************
004440
004450******************************************************************
004460**                MAIN ROUTINE                                  **
004470******************************************************************
004480 PROCEDURE DIVISION USING RMESSAGE,
004490                          CASD1-DATA,
004500                          RTN-OUTPUT-FIELDS.
004510
004520     IF FDP-DEBUG-RUNSTAT > '00'                                  FDM3514
004530        ADD +1  TO RTN-DIMT-CNT                                   FDM3514
004540        MOVE WHEN-COMPILED TO                                     FDM3514
004550             RTN-DIMT-COMPILE-DATE (RTN-DIMT-CNT)                 FDM3514
004560        MOVE WS-REVISION (16:10) TO                               FDM3514
004570             RTN-DIMT-REVISION (RTN-DIMT-CNT)                     FDM3514
004580        MOVE 'CACBLTUB' TO                                        FDM3514
004590           RTN-DIMT-PROGRAM-NAME (RTN-DIMT-CNT)                   FDM3514
004600        CALL 'CUTDIMTD' USING RMESSAGE,                           FDM3514
004610                              RTN-OUTPUT-FIELDS                   FDM3514
004620     END-IF.                                                      FDM3514
004630
004640     PERFORM A000-INITIALIZE
004650        THRU A000-EXIT.
004660
004670     PERFORM B000-MAIN
004680        THRU B000-EXIT.
004690
004700*    IF A ERROR OCCURRED CALL THE CD2COACD DAL
004710*    TO ERASE ALL OF THE COAC'S THAT WERE ROLLED FORWARD
004720*    BY THE A200- PARAGRAPH
004730*    SET ALL PROCESSED SCHED ACTIVITY RECORDS TO UNPROCESSED
004740     IF RTN-ERROR
004750        PERFORM Z300-ROLLBACK-COMPANY-ACCOUNTS THRU Z300-EXIT
004760        PERFORM Z400-UPDATE-SCHED-ACT-STATUS   THRU Z400-EXIT
004770        CALL 'CCOMMITD'
004780     END-IF.
004790     IF RTN-ERROR   AND  WS-RPTREQUEST = 1
004800        PERFORM Z320-DELETE-PRINTDATA          THRU Z320-EXIT
004810        PERFORM Z340-DELETE-REPORT-REQUEST     THRU Z340-EXIT
004820        CALL 'CCOMMITD'
004830     END-IF.
004840
004850* IF NO ERRORS - RESTORE ORIGINAL VALUE OF BATCH TR-REF-NO & SCAT
004860* IF ERRORS - WANT TO REPORT UNDER SPECIFIC DD_HIST SCAT/TR-REF
004870     IF RTN-NO-ERROR
004880        MOVE WS-ORIG-FDP-SCAT-KEY  TO  FDP-SCAT-KEY
004890        MOVE WS-ORIG-FDP-TR-REF-NO TO  FDP-TR-REF-NO
004900     END-IF.
004910
004920     GOBACK.
004930
004940 A000-INITIALIZE.
004950*********************************************************
004960**                                                     **
004970*********************************************************
004980*    HOLD THE ORIGINAL FDP INFORMATION
015500     MOVE FDP-SCAT-KEY    TO  WS-ORIG-FDP-SCAT-KEY.
015600     MOVE FDP-TR-REF-NO   TO  WS-ORIG-FDP-TR-REF-NO.
015400
009600     MOVE  ZERO           TO  WS-ERR-ACCT-KEY.
009600     MOVE  SPACES         TO  WS-ERR-ACCT-NO.
009600     MOVE  SPACES         TO  WS-ERR-SUB-ACCT-NO.
009600     MOVE  ZERO           TO  WS-ERR-BARL-KEY.
009600     MOVE  SPACES         TO  WS-ERR-DESCRIPT.
009600     MOVE  SPACES         TO  WS-ERR-NXT-BAL-DT.
015400
005090*    DO THIS UPDATE ONCE
005100     PERFORM Z000-BATCH-REPORTING THRU Z000-EXIT.
029500**   SET UP SCHED ACTIVITY TO ERROR FOR START UP PROCESSING
005120     MOVE 1                     TO WS-PROCESS-FLAG.
005130
005140*    CREATE SCHED ACTIVITY HERE TO CATCH STARTUP ERRORS
038600     PERFORM Z100-INSERT-SCHED-ACTIVITY THRU Z100-EXIT.
015600     MOVE WS-ORIG-FDP-TR-REF-NO TO FDP-TR-REF-NO.
005170
005180** EDIT INCOMING PARAMETERS
005190     PERFORM A010-EDIT-LINKAGE                                    FLM0001
005200        THRU A010-EXIT.                                           FLM0001
005210
005220** CALCULATE MONTH END DATE
005230     IF RTN-NO-ERROR
005240        PERFORM A100-CALC-MONTHEND-DATE  THRU  A100-EXIT
005250     END-IF.
005260
005270** verify dates
005280     IF RTN-NO-ERROR
019000        PERFORM A110-VERIFY-DATES  THRU  A110-EXIT
005300     END-IF.
005310
005320     IF RTN-NO-ERROR                                              FLM0001
005330*       CHECK FOR MULTI-CURRENCY                                  FLM0001
005340        PERFORM A120-CHECK-ISO-CURR                               FLM0001
005350           THRU A120-EXIT                                         FLM0001
005360     END-IF.                                                      FLM0001
005370
019300** SELECT COMPANY ACCOUNTS FROM LAST BALANCE RUN
005390     IF RTN-NO-ERROR
005400        PERFORM A200-FETCH-COMPANY-ACCOUNTS  THRU  A200-EXIT
005410     END-IF.
005420
005430     IF RTN-NO-ERROR
005440        PERFORM A300-SELECT-RPTREFID  THRU  A300-EXIT
005450     END-IF.
005460
005470     IF RTN-NO-ERROR
005480        PERFORM A400-INS-RPTREQUEST   THRU  A400-EXIT
005490     END-IF.
005500
005510     IF RTN-NO-ERROR
020100        PERFORM A500-CREATE-REPORT-HEADING THRU A500-EXIT
005530     END-IF.
005540
029500**   UPDATE THE SCHED ACTIVITY FOR STARTUP PROCESSING
054000     PERFORM Z200-UPDATE-SCHED-ACTIVITY THRU Z200-EXIT.
029500**   SET UP SCHED ACTIVITY TO ERROR FOR MAIN PROCESSING
005580     MOVE 2                     TO WS-PROCESS-FLAG.
005590
005600 A000-EXIT.
005610     EXIT.
005620
005630**********************************************************
005640*  EDIT LINKAGE                                                   FLM0001
005650**********************************************************
005660
005670 A010-EDIT-LINKAGE.                                               FLM0001
005680
005690     IF FDP-USER = SPACES
005700        MOVE 'UT'            TO RTN-ERR-PREFIX
005710        MOVE 39              TO RTN-ERROR-CODE
005720        MOVE 'CACBLTUB'      TO RTN-PROG-NAME
005730        MOVE 'A010-'         TO RTN-PARA-NAME                     FLM0001
005740        CALL 'CUTSVARB' USING RMESSAGE,
005750                              RTN-OUTPUT-FIELDS
005760     END-IF.
005770
005780     IF FDP-TR-REF-NO = SPACES AND RTN-NO-ERROR                   FLM0001
005790        MOVE 'UT'            TO RTN-ERR-PREFIX
005800        MOVE 34              TO RTN-ERROR-CODE
005810        MOVE 'CACBLTUB'      TO RTN-PROG-NAME
005820        MOVE 'A010-'         TO RTN-PARA-NAME                     FLM0001
005830        CALL 'CUTSVARB' USING RMESSAGE,
005840                              RTN-OUTPUT-FIELDS
005850     END-IF.
005860
005870     IF FDP-CYC-DT    = SPACES AND RTN-NO-ERROR                   FLM0001
005880        MOVE 'UT'            TO RTN-ERR-PREFIX
005890        MOVE 36              TO RTN-ERROR-CODE
005900        MOVE 'CACBLTUB'      TO RTN-PROG-NAME
005910        MOVE 'A010-'         TO RTN-PARA-NAME                     FLM0001
005920        CALL 'CUTSVARB' USING RMESSAGE,
005930                              RTN-OUTPUT-FIELDS
005940     END-IF.
005950
005960 A010-EXIT.                                                       FLM0001
005970   EXIT.                                                          FLM0001
005980
005990 A100-CALC-MONTHEND-DATE.
006000*********************************************************
006010* GET LAST MONTH ACCOUNT BALANCE DATE FROM SYSTEM_DATA
006020*********************************************************
006030
006040     CALL 'CS1SYSDD' USING RMESSAGE,
006050                           SYSD1-DATA,
006060                           RTN-OUTPUT-FIELDS.
006070
006080* VALIDATE THE LAST MONTH ACCOUNT BALANCE DATE FROM SYSTEM_DATA
006090
006100     IF RTN-NO-ERROR
006110        CALL 'CS2SYSDD' USING RMESSAGE,
006120                              SYSD1-DATA,
006130                              RTN-OUTPUT-FIELDS
006140     END-IF.
006150**
006160
006170*    CALCULATE NEXT MONTH END DATE, SAVE IN WORKING STORAGE
006180     IF RTN-NO-ERROR
006190        INITIALIZE DATEPARMS
006200        MOVE 110 TO REQUEST-TYPE
006210        MOVE SYSD1-LAST-ACCT-BAL-DT TO INDATE
006220        MOVE SYSD1-LAST-ACCT-BAL-DT TO WS-BEG-ACCT-BAL-DT
006230        MOVE +1 TO MONTHS-DAY-CHG
006240
006250        CALL 'CUTDTCAB' USING RMESSAGE,                           LSM5259
006260                              DATEPARMS,                          LSM5259
006270                              RTN-OUTPUT-FIELDS                   LSM5259
006280
006290        IF LK-ERROR-CD NOT = ZERO
006300           MOVE 'UT'        TO RTN-ERR-PREFIX
006310           MOVE 1100        TO RTN-ERROR-CODE
006320           MOVE 'CACBLTUB'  TO RTN-PROG-NAME
006330           MOVE 'A100-CALC' TO RTN-PARA-NAME
006340           CALL 'CUTSVARB' USING RMESSAGE,
006350                                RTN-OUTPUT-FIELDS
006360           DISPLAY 'ERROR IN DATE CALCULATION PROGRAM'
006370           DISPLAY 'CALLED FROM CACBLTUB'
006380        ELSE
006390           MOVE ODATE TO WS-END-ACCT-BAL-DT
006400        END-IF
006410     END-IF.
006420
006430 A100-EXIT.
006440     EXIT.
006450
006460 A110-VERIFY-DATES.
006470*********************************************************
006480* VERIFY DATES
006490*********************************************************
006500
021800*    ISSUE A WARNING IF CURRENT DATE < MONTH END DATE
021900     IF FDP-CYC-DT  <    WS-END-ACCT-BAL-DT
006530        MOVE 'TX'          TO RTN-ERR-PREFIX
006540        MOVE 1007          TO RTN-ERROR-CODE
006550        MOVE 'CACBLTUB'    TO RTN-PROG-NAME
006560        MOVE 'A110-VERFIY' TO RTN-PARA-NAME
006570        CALL 'CUTSVARB' USING RMESSAGE,
006580                              RTN-OUTPUT-FIELDS
006590     END-IF.
006600
020300*    READ BAL_ACCT_RULES
006620     INITIALIZE BARL2-DATA.
006630     PERFORM UNTIL BARL2-END-FLAG = 'Y'  OR  RTN-ERROR
006640        CALL 'CF1BARLD' USING RMESSAGE,
006650                              BARL2-DATA,
006660                              RTN-OUTPUT-FIELDS
020400
020400*      LOOP THRU ALL BARLS, CREATE ONE OR MORE VARIANCES
006810        PERFORM VARYING IBARL FROM 1 BY 1
006820                UNTIL IBARL > BARL2-ROWS-FOUND OR
006820                      RTN-ERROR
006720
021800*           ISSUE A ERROR IF UNPROCESSED
021800*           BAL_ACCT_RULE IS FOUND.
021800*           ADD VARIANCE DETAIL
021900            IF BARL2-STAT-CD (IBARL) = '00'
006770               MOVE 'TX'          TO RTN-ERR-PREFIX
006780               MOVE 1008          TO RTN-ERROR-CODE
006790               MOVE 'CACBLTUB'    TO RTN-PROG-NAME
006800               MOVE 'A110-VERIFY' TO RTN-PARA-NAME
006810
007490               MOVE BARL2-BARL-KEY (IBARL)   TO
007500                    WS-ERR-BARL-KEY
007490*              ONLY MOVE FIRST 25 CHARACTERS
007490               MOVE BARL2-DESCRIPT (IBARL) (1:25) TO
007500                    WS-ERR-DESCRIPT
007510               MOVE BARL2-NXT-BAL-DT (IBARL) TO
007520                    WS-ERR-NXT-BAL-DT
007530               MOVE WS-ERR-MSG4      TO RTN-VAR-TEXT   (1)                                            000167300
007540               MOVE 1                TO RTN-VAR-SEQ-NO (1)                                          000167400
007550               MOVE WS-ERR-MSG5      TO RTN-VAR-TEXT   (2)                                            000167500
007560               MOVE 2                TO RTN-VAR-SEQ-NO (2)                                          000167600
007550               MOVE WS-ERR-MSG6      TO RTN-VAR-TEXT   (3)                                            000167500
007560               MOVE 3                TO RTN-VAR-SEQ-NO (3)                                          000167600
006950               MOVE 3                TO RTN-VAR-DETL-CNT          LMM1120                          000167600
006960
006970               CALL 'CUTSVARB' USING RMESSAGE,
006980                                     RTN-OUTPUT-FIELDS
006990            END-IF
019600
007121          END-PERFORM
007121     END-PERFORM.
007030
007040 A110-EXIT.
007050     EXIT.
007060
007070**********************************************************        FLM0001
007080*  CHECK FOR MULTIPLE CURRENCY CODES ON JLOGS TO BALANCE          FLM0001
007090**********************************************************        FLM0001
007100
007110 A120-CHECK-ISO-CURR.                                             FLM0001
007120
007130     INITIALIZE JLOG1-DATA.                                       FLM0001
007140     MOVE WS-BEG-ACCT-BAL-DT     TO  JLOG1-BEGIN-DT.              FLM0001
007150     MOVE WS-END-ACCT-BAL-DT     TO  JLOG1-END-DT.                FLM0001
007160     CALL 'CS6JLOGD' USING RMESSAGE,                              FLM0001
007170                           JLOG1-DATA,                            FLM0001
007180                           RTN-OUTPUT-FIELDS.                     FLM0001
007190
007200     IF RTN-NO-ERROR                                              FLM0001
007210        IF JLOG1-ROWS-FOUND > 1                                   FLM0001
007220           MOVE 'UT'    TO RTN-ERR-PREFIX                         FLM0001
007230           MOVE 317     TO RTN-ERROR-CODE                         FLM0001
007240           MOVE 'CACBLTUB' TO RTN-PROG-NAME                       FLM0001
007250           MOVE 'A120'  TO RTN-PARA-NAME                          FLM0001
007260           MOVE WS-BEG-ACCT-BAL-DT   TO WS-ERR-BEG-BAL-DT         FLM0001
007270           MOVE WS-END-ACCT-BAL-DT   TO WS-ERR-END-BAL-DT         FLM0001
007280           MOVE WS-ERR-MSG7          TO RTN-VAR-TEXT   (1)        FLM0001                             000167300
007290           MOVE 1                    TO RTN-VAR-SEQ-NO (1)        FLM0001                           000167400
007300           MOVE WS-ERR-MSG8          TO RTN-VAR-TEXT   (2)        FLM0001                             000167500
007310           MOVE 2                    TO RTN-VAR-SEQ-NO (2)        FLM0001                           000167600
007320           MOVE 2                    TO RTN-VAR-DETL-CNT          FLM0001
007330           CALL 'CUTSVARB' USING RMESSAGE,                        FLM0001
007340                                 RTN-OUTPUT-FIELDS                FLM0001
007350        END-IF                                                    FLM0001
007360     END-IF.                                                      FLM0001
007370
007380 A120-EXIT.                                                       FLM0001
007390     EXIT.
007400**********************************************************
007410
007420 A200-FETCH-COMPANY-ACCOUNTS.
007430*********************************************************
023700* SELECT ALL COMPANY ACCOUNTS FROM LAST MONTH
007450*********************************************************
007460     INITIALIZE COAC2-DATA.
007470     PERFORM UNTIL COAC2-END-FLAG = 'Y'  OR  RTN-ERROR
024100       MOVE WS-BEG-ACCT-BAL-DT TO COAC2-ACCT-BAL-DT
007490       CALL 'CF1COACD' USING RMESSAGE,
007500                             COAC2-DATA,
024500                             RTN-OUTPUT-FIELDS
006430
006440       IF RTN-NO-ERROR
006450          IF COAC2-ROWS-FOUND > 0
006460             PERFORM A210-PROCESS-COMPANY-ACCTS THRU A210-EXIT
006470          END-IF
006480       END-IF
006430
026300     END-PERFORM.
006430
007610 A200-EXIT.
007620     EXIT.
007630
023500 A210-PROCESS-COMPANY-ACCTS.
007650*********************************************************
023700* LOOP THRU ALL COMPANY ACCOUNTS FROM LAST MONTH
023700* INSERT NEW ONES FOR THIS RUN
007680*********************************************************
006810     PERFORM VARYING ICOAC FROM 1 BY 1
006820             UNTIL ICOAC > COAC2-ROWS-FOUND OR
006840                           RTN-ERROR
007110
007110        INITIALIZE COAC1-DATA
007110        MOVE COAC2-ACCT-KEY (ICOAC)      TO  COAC1-ACCT-KEY
007110        MOVE COAC2-END-BAL-AMT (ICOAC)   TO  COAC1-BEG-BAL-AMT
024200        MOVE WS-END-ACCT-BAL-DT          TO  COAC1-EFF-DT
007110        MOVE COAC2-END-BAL-AMT (ICOAC)   TO  COAC1-END-BAL-AMT
007110        MOVE COAC2-CASE-KEY (ICOAC)      TO  COAC1-CASE-KEY
007110        MOVE COAC2-MKEY-CO-NUM (ICOAC)   TO  COAC1-MKEY-CO-NUM
007110        MOVE COAC2-MKEY-PROD-NUM (ICOAC) TO  COAC1-MKEY-PROD-NUM
007110        MOVE COAC2-PROD-TYP-CD (ICOAC)   TO  COAC1-PROD-TYP-CD
007110        MOVE FDP-TR-REF-NO               TO  COAC1-TR-REF-NO
007110
007840        CALL 'CI3COACD' USING RMESSAGE,
007850                              COAC1-DATA,
007860                              RTN-OUTPUT-FIELDS
007870
007880     END-PERFORM.
007890
007900 A210-EXIT.
007910     EXIT.
007920
007930 A300-SELECT-RPTREFID.
007940*********************************************************
007950*
007960*********************************************************
007970
007980**   SELECT RPTREFID  FROM  RPTDEF,RPTVERS,RPTREFLNK JOIN
007990
008000     MOVE '9001' TO VRPD1-TRANID.
008010
008020     CALL 'CS1VRPDD' USING RMESSAGE,
008030                           VRPD1-DATA,
008040                           RTN-OUTPUT-FIELDS.
008050
008060     IF VRPD1-ROWS-FOUND  =  ZERO
008070        MOVE 'BT'              TO RTN-ERR-PREFIX
008080        MOVE 1216              TO RTN-ERROR-CODE
008090        MOVE 'CACBLTUB'        TO RTN-PROG-NAME
011700        MOVE 'A300'            TO RTN-PARA-NAME
008110        CALL 'CUTSVARB' USING RMESSAGE,
008120                              RTN-OUTPUT-FIELDS
008130     END-IF.
008140
008150 A300-EXIT.
008160      EXIT.
008170
008180
008190 A400-INS-RPTREQUEST.
008200*********************************************************
008210*
008220*********************************************************
008230
008240     INITIALIZE RPRQ1-DATA.                                       FDM1848
008250     MOVE  VRPD1-RPTREFID      TO     RPRQ1-RPTREFID.
008260     MOVE  FDP-CYC-DT          TO     RPRQ1-SCHEDDT.
018800* 3 = SET REPORT REQUEST TO PROCESSED STATE
008280     MOVE  '3'                 TO     RPRQ1-STATCD.
008290     MOVE  ZEROES              TO     RPRQ1-AUDITID.
008300     MOVE  ZEROES              TO     RPRQ1-RPTREQUESTID.
008310     MOVE  ZEROES              TO     RPRQ1-RPSV-KEY.             FDM1848
008320     MOVE  SPACES              TO     RPRQ1-TR-REF-NO.            FDM1848
008330
008340**
008350**   INSERT INTO RPTREQUEST
008360**
008370     CALL 'CI1RPRQD' USING RMESSAGE,
008380                           RPRQ1-DATA,
008390                           RTN-OUTPUT-FIELDS.
008400
008410     IF RTN-NO-ERROR
008420        MOVE 1 TO WS-RPTREQUEST
008430     END-IF.
008440
008450 A400-EXIT.
008460      EXIT.
008470
008480 A500-CREATE-REPORT-HEADING.
008490*********************************************************
008500*
008510*********************************************************
011400     MOVE CHR34   TO  FILLER1  FILLER2  FILLER3
011400                      FILLER4  FILLER5  FILLER6.
008540*    INSERT A PRINTDATA RECORD FOR THE REPORT HEADING
006913     MOVE WS-BEG-ACCT-BAL-DT         TO  HEAD-LAST-ACCT-BAL-DT.
006913     MOVE WS-END-ACCT-BAL-DT         TO  HEAD-NEW-ACCT-BAL-DT.
006913     MOVE FDP-CYC-DT                 TO  HEAD-CYC-DT.
008580
008590     ADD    +1    TO    WS-SEQ-NUM.
008600
008610     MOVE  RPRQ1-RPTREQUESTID        TO  PRDT1-RPTREQUESTID.
008620     MOVE  WS-SEQ-NUM                TO  PRDT1-SEQNUM.
008630     MOVE  HEAD-REC                  TO  PRDT1-CDL.
008640     MOVE  'N'                       TO  PRDT1-CONTINUECD.
008650     MOVE  ZEROES                    TO  PRDT1-AUDITID.
008660     MOVE 'HEAD'                     TO  PRDT1-RECORDTYPE.
008670** CHANGED FROM SPACES TO 00 THIS WILL SORT HEADER RECORD TO TOP
008680     MOVE '00'                       TO  PRDT1-SORTDATA.
008690
008700     CALL 'CI1PRDTD' USING RMESSAGE,
008710                           PRDT1-DATA,
008720                           RTN-OUTPUT-FIELDS.
008730*
008740 A500-EXIT.
008750      EXIT.
008760
008770 B000-MAIN.
008780*********************************************************
008790* MAIN LOOP TO PROCESS JOURNAL LOG RECORDS
008800* ORDERED BY ACCT-KEY,MKEY-CO-NUM,MKEY-PROD-NUM,CASE-KEY
008810* MAJOR BREAK ON ACCOUNT KEY
008820* MINOR BREAK ON COMPANY, PRODUCT AND CASE WITHIN A ACCT-KEY
008830*********************************************************
008840     INITIALIZE WS-HOLD-VARIABLES.
008850     INITIALIZE JLOG4-DATA.
008860     INITIALIZE WS-CODES.
008870     INITIALIZE WS-ACCUMULATORS.
008880
008890     PERFORM UNTIL JLOG4-END-FLAG = 'Y'  OR  RTN-ERROR
008900
008910*       FETCH NEXT BLOCK OF JOURNAL LOGS
008920        MOVE WS-BEG-ACCT-BAL-DT  TO  JLOG4-BEGIN-DT
008930        MOVE WS-END-ACCT-BAL-DT  TO  JLOG4-END-DT
008940        CALL 'CF1VJLLD'    USING RMESSAGE,
008950                                 JLOG4-DATA,
008960                                 RTN-OUTPUT-FIELDS
008970
008980**      SETUP FOR FIRST TIME THRU
008990        IF JLOG4-CUMUL-ROWS >  0  AND
009000           JLOG4-CUMUL-ROWS <= 50
009010*          SETUP HOLD AREAS PREVENT INITIAL BREAK
009020           MOVE JLOG4-ACCT-KEY (1)      TO WS-HOLD-ACCT-KEY
009030           MOVE JLOG4-MKEY-CO-NUM (1)   TO WS-HOLD-CO-NUM
009040           MOVE JLOG4-MKEY-PROD-NUM (1) TO WS-HOLD-PROD-NUM
009050           MOVE JLOG4-CASE-KEY (1)      TO WS-HOLD-CASE-KEY
009060           MOVE 1 TO IJLOG
009070           PERFORM B100-GET-LEDGER-ACCOUNT THRU B100-EXIT
009080           MOVE ACCT1-BAL-LVL-CD        TO WS-BALANCE-LEVEL
009090           PERFORM B150-DELETE-COMPANY-ACCTS THRU B150-EXIT
009100        END-IF
009110
009120**      PROCESS EACH JOURNAL LOG RECORD
009130**
009140        PERFORM VARYING IJLOG FROM 1 BY 1
009150                UNTIL IJLOG > JLOG4-ROWS-FOUND OR  RTN-ERROR
009160
009170*          LEDGER_ACCOUNT KEY BREAK
009180           IF JLOG4-ACCT-KEY (IJLOG) = WS-HOLD-ACCT-KEY
009190              CONTINUE
009200           ELSE
009210**           LEDGER ACCT CHANGED, PROCESS DATA FROM PRIOR ACCT
009220             PERFORM B700-WRITE-COMPANY-ACCT-TOTAL
009230                THRU B700-EXIT
009240**           UPDATE THE SCHED ACTIVITY FOR PRIOR ACCOUNT
009250             PERFORM Z200-UPDATE-SCHED-ACTIVITY THRU Z200-EXIT
009260**           CREATE A NEW SCHED ACTIVITY FOR THIS ACCOUNT IN B100
009270             IF RTN-NO-ERROR
009280               PERFORM B100-GET-LEDGER-ACCOUNT THRU B100-EXIT
009290               INITIALIZE WS-HOLD-VARIABLES
009300               INITIALIZE WS-CODES
009310               INITIALIZE WS-ACCUMULATORS
009320*              SETUP HOLD AREAS PREVENT INITIAL BREAK
009330               MOVE JLOG4-ACCT-KEY (IJLOG)     TO WS-HOLD-ACCT-KEY
009340               MOVE JLOG4-MKEY-CO-NUM (IJLOG)  TO WS-HOLD-CO-NUM
009350               MOVE JLOG4-MKEY-PROD-NUM(IJLOG) TO WS-HOLD-PROD-NUM
009360               MOVE JLOG4-CASE-KEY (IJLOG)     TO WS-HOLD-CASE-KEY
009370               MOVE ACCT1-BAL-LVL-CD           TO WS-BALANCE-LEVEL
009380               PERFORM B150-DELETE-COMPANY-ACCTS THRU B150-EXIT
009390             END-IF
009400           END-IF
009410
009420**      CHECK THE BALANCING LEVEL & PROCESS JLOG RECORD
009430
009440**         ISSUE ONE WARNING IF THE BALANCE LEVEL IS NOT SET
009450           IF NOT-APPLICABLE  AND  RTN-NO-ERROR
009460                              AND  WARNING-NOT-ISSUED
009470              DISPLAY 'ERROR BAL-LVL-CD SHOULD BE SET'
009480                      ' ACCT1-ACCT-KEY = ' ACCT1-ACCT-KEY
009490              MOVE 'TX'            TO RTN-ERR-PREFIX
009500              MOVE 1002            TO RTN-ERROR-CODE
009510              MOVE 'CACBLTUB'      TO RTN-PROG-NAME
009520              MOVE 'B000'          TO RTN-PARA-NAME
009530              MOVE ACCT1-ACCT-KEY  TO WS-ERR-ACCT-KEY
009540              MOVE 1               TO RTN-VAR-SEQ-NO (1)
009550              MOVE WS-ERR-MSG1     TO RTN-VAR-TEXT (1)
009560              MOVE 1               TO RTN-VAR-DETL-CNT
009570              CALL 'CUTSVARB' USING RMESSAGE,
009580                           RTN-OUTPUT-FIELDS
009590              MOVE 1               TO WS-WARNING-FLAG
009600           END-IF
009610
009620           IF LEDGER-LEVEL  AND   RTN-NO-ERROR
009630**            ONE BREAK CONDITION / LEDGER ACCOUNT NUMBER CHANGED
009640              PERFORM B600-PROCESS-DEBIT-CREDIT THRU B600-EXIT
009650           END-IF
009660
009670           IF COMPANY-LEVEL AND   RTN-NO-ERROR
009680              PERFORM B300-COMPANY-LEVEL THRU B300-EXIT
009690           END-IF
009700
009710           IF PRODUCT-LEVEL AND   RTN-NO-ERROR
009720              PERFORM B400-PRODUCT-LEVEL THRU B400-EXIT
009730           END-IF
009740
009750           IF CASE-LEVEL    AND   RTN-NO-ERROR
009760              PERFORM B500-CASE-LEVEL THRU B500-EXIT
009770           END-IF
009780
009790        END-PERFORM
009800     END-PERFORM.
009810
035100*    ISSUE A WARNING IF NO JOURNAL LOGS WERE PROCESSED
009830     IF RTN-NO-ERROR  AND  JLOG4-CUMUL-ROWS = 0
009840        MOVE 'TX'            TO RTN-ERR-PREFIX
009850        MOVE 1013            TO RTN-ERROR-CODE
009860        MOVE 'CACBLTUB'      TO RTN-PROG-NAME
009870        MOVE 'B000'          TO RTN-PARA-NAME
009880        CALL 'CUTSVARB' USING RMESSAGE,
009890                             RTN-OUTPUT-FIELDS
009900     END-IF.
009910
009920*    FLUSH OUT TOTALS FOR LAST RECORD
009930     IF RTN-NO-ERROR  AND  JLOG4-CUMUL-ROWS > 0
009940        PERFORM B700-WRITE-COMPANY-ACCT-TOTAL
009950           THRU B700-EXIT
009960     END-IF.
009970
009980*    FLUSH OUT SCHED ACTIVITY FOR LAST RECORD
009990     IF JLOG4-CUMUL-ROWS > 0
010000        PERFORM Z200-UPDATE-SCHED-ACTIVITY THRU Z200-EXIT
010010     END-IF.
010020
010030     IF RTN-NO-ERROR
010040*       UPDATE SYSTEM_DATA WITH THE NEW ACCOUNT BALANCE DATE
010050        MOVE WS-END-ACCT-BAL-DT  TO  SYSD1-LAST-ACCT-BAL-DT
010060        CALL 'CU1SYSDD' USING RMESSAGE,
010070                              SYSD1-DATA,
010080                              RTN-OUTPUT-FIELDS
010090     END-IF.
010100
010110     IF RTN-NO-ERROR  AND  JLOG4-CUMUL-ROWS > 0
010120*       UPDATE BLOCK OF JOURNAL_LOGS WITH HOLD_CD = 2 BATCH BAL
010130        INITIALIZE JLOG5-DATA
010140*       FETCH NEXT BLOCK OF JOURNAL LOGS
010150        MOVE WS-BEG-ACCT-BAL-DT  TO  JLOG5-LAST-ACCT-BAL-DT1
010160        MOVE WS-END-ACCT-BAL-DT  TO  JLOG5-NEW-ACCT-BAL-DT
010170
010180
010190        MOVE '2'                 TO  JLOG5-HOLD-CD
010200        MOVE '0'                 TO  JLOG5-FROM-HOLD-CD
010210
010220
010230        CALL 'CU2JLOGD' USING RMESSAGE,
010240                              JLOG5-DATA,
010250                              RTN-OUTPUT-FIELDS
010260     END-IF.
010270
010280
010290*    RECORD THAT ACCOUNT BALANCING RAN SUCCESSFULLY IN
010300*    THE SCHED_ACCT_BAL TABLE
010310     IF RTN-NO-ERROR
010320         PERFORM B800-INSERT-SCHED-ACCT-BAL
010330            THRU B800-EXIT
010340     END-IF.
010350
010360  B000-EXIT.
010370     EXIT.
010380
010390
010400 B100-GET-LEDGER-ACCOUNT.
010410*********************************************************
010420** GET THE LEDGER ACCOUNT
010430** CREATE A SCHED ACTIVITY RECORD FOR THIS ACCOUNT
010440*********************************************************
010450     INITIALIZE ACCT1-DATA.
010460     MOVE JLOG4-ACCT-KEY (IJLOG) TO ACCT1-ACCT-KEY.
010470
010480     CALL 'CS2ACCTD'    USING RMESSAGE,
010490                              ACCT1-DATA,
010500                              RTN-OUTPUT-FIELDS.
010510
010520*    HOLD THIS INFORMATION FOR BATCH REPORTING
010530     MOVE ACCT1-ACCT-NO         TO WS-HOLD-ACCT-NO.
010540     MOVE ACCT1-SUB-ACCT-NO     TO WS-HOLD-SUB-ACCT-NO.
010550
010560     IF RTN-NO-ERROR
010570        PERFORM Z100-INSERT-SCHED-ACTIVITY THRU Z100-EXIT
010580     END-IF.
010590
010600 B100-EXIT.
010610     EXIT.
010620
010630 B150-DELETE-COMPANY-ACCTS.
010640*********************************************************
010650* DELETE SELECTED COMPANY ACCOUNTS THAT WERE ROLLED FORWARD BY
010660* A200-INSERT-COMPANY-ACCOUNTS.  ALL COMPANY ACCOUNTS
010670* BROUGHT FORWARD THAT DO NOT MATCH THIS MONTH'S LEVEL OF
010680* BALANCING WILL BE DELETED.
010690* COMPANY, PRODUCT, AND CASE VALUES ARE NOT USED FOR A
010700* MATCH CONDITION.  THEY ARE USED BY THE DAL TO DETERMINE THE
010710* LEVEL OF BALANCING.
010720*********************************************************
010730     IF NOT-APPLICABLE
010740        GO TO B150-EXIT
010750     END-IF.
010760
010770     INITIALIZE COAC1-DATA.
010780     IF COMPANY-LEVEL
010790        MOVE  1  TO  COAC1-MKEY-CO-NUM
010800     END-IF.
010810     IF PRODUCT-LEVEL
010820        MOVE  1  TO  COAC1-MKEY-PROD-NUM
010830     END-IF.
010840     IF CASE-LEVEL
010850        MOVE  1  TO  COAC1-CASE-KEY
010860     END-IF.
010870
010880     MOVE WS-END-ACCT-BAL-DT TO COAC1-ACCT-END-BAL-DT.
010890     MOVE ACCT1-ACCT-KEY     TO COAC1-ACCT-KEY.
010900
010910     MOVE FDP-TR-REF-NO          TO WS-NEW-FDP-TR-REF-NO.
010920     MOVE WS-ORIG-FDP-TR-REF-NO  TO FDP-TR-REF-NO.
010930
010940     CALL 'CD1COACD' USING RMESSAGE,
010950                           COAC1-DATA,
010960                           RTN-OUTPUT-FIELDS.
010970
010980     MOVE WS-NEW-FDP-TR-REF-NO   TO FDP-TR-REF-NO.
010990
011000 B150-EXIT.
011010    EXIT.
011020
011030 B300-COMPANY-LEVEL.
011040*********************************************************
011050** TWO BREAK CONDITIONS
011060** LEDGER ACCOUNT NUMBER CHANGED  ( HANDLED IN B600 )
011070** COMPANY NUM CHANGED WITHIN A LEDGER ACCOUNT
011080*********************************************************
011090     IF JLOG4-MKEY-CO-NUM (IJLOG) = WS-HOLD-CO-NUM
011100        PERFORM B600-PROCESS-DEBIT-CREDIT THRU B600-EXIT
011110     ELSE
011120*       WRITE OUT TOTALS FOR PREVIOUS ACCOUNT/COMPANY
011130        PERFORM B700-WRITE-COMPANY-ACCT-TOTAL THRU B700-EXIT
011140*       CLEAR ACCUMULATORS, GET READY FOR NEXT COMPANY
011150        INITIALIZE WS-ACCUMULATORS
011160        MOVE JLOG4-MKEY-CO-NUM (IJLOG) TO WS-HOLD-CO-NUM
011170*       ADD TO ACCUMULATORS DATA FROM CURRENT RECORD NEW COMPANY
011180        PERFORM B600-PROCESS-DEBIT-CREDIT THRU B600-EXIT
011190     END-IF.
011200
011210 B300-EXIT.
011220     EXIT.
011230
011240 B400-PRODUCT-LEVEL.
011250*********************************************************
011260** TWO BREAK CONDITIONS
011270** LEDGER ACCOUNT NUMBER CHANGED  ( HANDLED IN B600 )
011280** PRODUCT NUM CHANGED WITHIN A LEDGER ACCOUNT
011290*********************************************************
011300     IF JLOG4-MKEY-PROD-NUM (IJLOG) = WS-HOLD-PROD-NUM
011310        PERFORM B600-PROCESS-DEBIT-CREDIT THRU B600-EXIT
011320     ELSE
011330*       WRITE OUT TOTALS FOR PREVIOUS ACCOUNT/PRODUCT
011340        PERFORM B700-WRITE-COMPANY-ACCT-TOTAL THRU B700-EXIT
011350*       CLEAR ACCUMULATORS, GET READY FOR NEXT PRODUCT
011360        INITIALIZE WS-ACCUMULATORS
011370        MOVE JLOG4-MKEY-PROD-NUM (IJLOG) TO WS-HOLD-PROD-NUM
011380*       ADD TO ACCUMULATORS DATA FROM CURRENT RECORD NEW PRODUCT
011390        PERFORM B600-PROCESS-DEBIT-CREDIT THRU B600-EXIT
011400     END-IF.
011410 B400-EXIT.
011420     EXIT.
011430
011440 B500-CASE-LEVEL.
011450*********************************************************
011460** TWO BREAK CONDITIONS
011470** LEDGER ACCOUNT NUMBER CHANGED  ( HANDLED IN B600 )
011480** CASE KEY CHANGED WITHIN A LEDGER ACCOUNT
011490*********************************************************
011500     IF JLOG4-CASE-KEY (IJLOG) = WS-HOLD-CASE-KEY
011510        PERFORM B600-PROCESS-DEBIT-CREDIT THRU B600-EXIT
011520     ELSE
011530*       WRITE OUT TOTALS FOR PREVIOUS ACCOUNT/CASE
011540        PERFORM B700-WRITE-COMPANY-ACCT-TOTAL THRU B700-EXIT
011550*       CLEAR ACCUMULATORS, GET READY FOR NEXT CASE
011560        INITIALIZE WS-ACCUMULATORS
011570        MOVE JLOG4-CASE-KEY (IJLOG) TO WS-HOLD-CASE-KEY
011580*       ADD TO ACCUMULATORS DATA FROM CURRENT RECORD NEW CASE
011590        PERFORM B600-PROCESS-DEBIT-CREDIT THRU B600-EXIT
011600     END-IF.
011610
011620 B500-EXIT.
011630     EXIT.
011640
011650 B600-PROCESS-DEBIT-CREDIT.
011660*********************************************************
011670** PROCESS JOURNAL LOG AMOUNT
011680*********************************************************
011690     IF JLOG4-DB-CR-CD (IJLOG) = 'DB' OR
011700        JLOG4-DB-CR-CD (IJLOG) = 'DX'
011710           ADD JLOG4-JRNL-AMT (IJLOG) TO WS-TOTAL-DEBITS
011720     END-IF.
011730
011740     IF JLOG4-DB-CR-CD (IJLOG) = 'CR' OR
011750        JLOG4-DB-CR-CD (IJLOG) = 'CX'
011760           ADD JLOG4-JRNL-AMT (IJLOG) TO WS-TOTAL-CREDITS
011770     END-IF.
011780
011790 B600-EXIT.
011800     EXIT.
011810
011820 B700-WRITE-COMPANY-ACCT-TOTAL.
011830*********************************************************
011840** USE WS-HOLD-VARIABLE KEYS
011850*********************************************************
011860     IF NOT-APPLICABLE
011870        GO TO B700-EXIT
011880     END-IF.
011890
011900* CALCULATE TOTAL DEBITS AND CREDITS
011910     COMPUTE WS-DB-CR-TOTAL =
011920             WS-TOTAL-CREDITS - WS-TOTAL-DEBITS.
011930
011940* ATTEMPT TO UPDATE COMPANY ACCOUNT BY ACCT KEY/LEVEL
011950* OF BALANCE FIELD
011960* IF 1403 ERROR  (COAC DOES NOT EXIST  ROWS-FOUND=ZERO)
011970* CALL DAL PROGRAM TO SUMMARIZE ALL JOURNAL LOGS <= BEGIN BAL DT
011980* INSERT A COMPANY ACCOUNT WITH ACCT KEY/LEVEL OF BALANCE FIELD
011990* AND BALANCE AMOUNT
012000
012010* ATTEMPT UPDATE COMPANY ACCOUNT WITH CLOSING BALANCE
012020     IF RTN-NO-ERROR
012030        PERFORM B720-UPDATE-COMPANY-ACCT THRU B720-EXIT
012040     END-IF.
012050
012060     IF COAC1-ROWS-FOUND = ZEROES  AND  RTN-NO-ERROR
012070*       SUMMARIZE JOURNAL LOGS
012080        PERFORM B740-SUM-JLOGS           THRU B740-EXIT
012090        IF RTN-NO-ERROR
012100*          INSERT NEW COMPANY ACCOUNT
012110           PERFORM B760-INSERT-COMPANY-ACCT THRU B760-EXIT
012120        END-IF
012130        IF RTN-NO-ERROR
012140*          BUILD PRINT DATA BASED ON A INSERTED COMPANY ACCOUNT
053400           PERFORM B910-BUILD-PRINTDATA-INS
053400              THRU B910-EXIT
012170        END-IF
012180     ELSE
012190        IF RTN-NO-ERROR
012200*          BUILD PRINTDATA BASED ON A UPDATED COMPANY ACCOUNT
063500           PERFORM B900-BUILD-PRINTDATA-UPD THRU B900-EXIT
012220        END-IF
012230     END-IF.
012240
053300     IF RTN-NO-ERROR
063500        PERFORM B920-BUILD-WRITE-PRINTDATA THRU B920-EXIT
053500     END-IF.
012280
012290 B700-EXIT.
012300     EXIT.
012310
012320 B720-UPDATE-COMPANY-ACCT.
012330*********************************************************
012340* UPDATE BY ACCT, ACCT/CO, ACCT/PROD, OR ACCT/CASE
012350*********************************************************
012360     INITIALIZE COAC1-DATA.
012370     IF COMPANY-LEVEL
012380        MOVE WS-HOLD-CO-NUM   TO  COAC1-MKEY-CO-NUM
012390     END-IF.
012400     IF PRODUCT-LEVEL
012410        MOVE WS-HOLD-PROD-NUM  TO  COAC1-MKEY-PROD-NUM
012420     END-IF.
012430     IF CASE-LEVEL
012440        MOVE WS-HOLD-CASE-KEY  TO  COAC1-CASE-KEY
012450     END-IF.
012460
012470*    UPDATE COMPANY_ACCTS WITH THE ENDING BALANCE AMOUNT
012480*
012490     MOVE WS-HOLD-ACCT-KEY              TO  COAC1-ACCT-KEY.
012500     MOVE WS-END-ACCT-BAL-DT            TO  COAC1-EFF-DT.
012510     MOVE WS-DB-CR-TOTAL                TO  COAC1-TOT-END-BAL-AMT.
012520*    THE ACTUAL END BALANCE AMOUNT IS CALCULATED IN THE
012530*    DAL PROGRAM USING THE FOLLOWING FORMULA
012540*    COMPUTE COAC1-TOT-END-BAL-AMT =  COAC1-BEG-BAL-AMT PLUS
012550*                                     WS-DB-CR-TOTAL (PASSED IN)
012560
012570     MOVE FDP-TR-REF-NO          TO WS-NEW-FDP-TR-REF-NO.
012580     MOVE WS-ORIG-FDP-TR-REF-NO  TO FDP-TR-REF-NO.
012590
012600     CALL 'CU2COACD' USING RMESSAGE,
012610                           COAC1-DATA,
012620                           RTN-OUTPUT-FIELDS.
012630
012640     MOVE WS-NEW-FDP-TR-REF-NO   TO FDP-TR-REF-NO.
012650
012660 B720-EXIT.
012670     EXIT.
012680
012690 B740-SUM-JLOGS.
012700*********************************************************
012710* SUM BY ACCT, ACCT/CO, ACCT/PROD, OR ACCT/CASE
012720*********************************************************
012730     INITIALIZE JLOG3-DATA.
012740     IF COMPANY-LEVEL
012750        MOVE WS-HOLD-CO-NUM   TO  JLOG3-MKEY-CO-NUM
012760     END-IF.
012770     IF PRODUCT-LEVEL
012780        MOVE WS-HOLD-PROD-NUM  TO  JLOG3-MKEY-PROD-NUM
012790     END-IF.
012800     IF CASE-LEVEL
012810        MOVE WS-HOLD-CASE-KEY  TO  JLOG3-CASE-KEY
012820     END-IF.
012830*
012840     MOVE WS-HOLD-ACCT-KEY        TO  JLOG3-ACCT-KEY.
012850     MOVE SYSD1-LAST-ACCT-BAL-DT  TO  JLOG3-LAST-ACCT-BAL-DT1.
012860
012870     CALL 'CS2JLOGD' USING RMESSAGE,
012880                           JLOG3-DATA,
012890                           RTN-OUTPUT-FIELDS.
012900
012910 B740-EXIT.
012920     EXIT.
012930
012940 B760-INSERT-COMPANY-ACCT.
012950*********************************************************
012960* INSERT NEW COAC RECORD POPULATE FIELDS BASED ON LEVEL
012970* OF BALANCING
012980*********************************************************
012990     INITIALIZE COAC1-DATA.
013000     IF COMPANY-LEVEL
013010        MOVE WS-HOLD-CO-NUM   TO  COAC1-MKEY-CO-NUM
013020     END-IF.
013030     IF PRODUCT-LEVEL
013040        MOVE WS-HOLD-PROD-NUM  TO  COAC1-MKEY-PROD-NUM
013050     END-IF.
013060     IF CASE-LEVEL
013070        MOVE WS-HOLD-CASE-KEY  TO  COAC1-CASE-KEY
013080     END-IF.
013090
013100     MOVE WS-HOLD-ACCT-KEY              TO  COAC1-ACCT-KEY.
013110     MOVE WS-END-ACCT-BAL-DT            TO  COAC1-EFF-DT.
013120     MOVE WS-ORIG-FDP-TR-REF-NO         TO  COAC1-TR-REF-NO.
013130*    SUM OF ALL JLOGS <= BEGIN-BAL-DATE
013140*    PLUS THE SUM OF JLOGS >BEGIN-BAL-DT AND <= END-BAL-DT
013150     ADD  JLOG3-DB-CR-TOTAL             TO  WS-DB-CR-TOTAL.
013160     MOVE WS-DB-CR-TOTAL                TO  COAC1-END-BAL-AMT.
013170
013180     CALL 'CI1COACD' USING RMESSAGE,
013190                           COAC1-DATA,
013200                           RTN-OUTPUT-FIELDS.
013210
013220 B760-EXIT.
013230     EXIT.
013240
013250
013260 B800-INSERT-SCHED-ACCT-BAL.
013270*********************************************************
013280**   INSERT ONE RECORD IN THE SCHED_ACCT_BAL TABLE FOR THE
013290**   ACCOUNT BALANCING JOB
013300*********************************************************
013310     INITIALIZE SCAB1-DATA.
013320     MOVE '02'                 TO SCAB1-BAL-TYP-CD.
013330     MOVE WS-END-ACCT-BAL-DT   TO SCAB1-EFF-DT.
013340     MOVE 0                    TO SCAB1-REAR-KEY.
013350     MOVE '01'                 TO SCAB1-STAT-CD.
013360     MOVE WS-ORIG-FDP-TR-REF-NO TO SCAB1-TR-REF-NO.
013370
013380     CALL 'CI1SCABD' USING RMESSAGE,
013390                           SCAB1-DATA,
013400                           RTN-OUTPUT-FIELDS.
013410
013420 B800-EXIT.
013430     EXIT.
013440
013450
013460 B900-BUILD-PRINTDATA-UPD.
013470*********************************************************
013480**   BUILD PRINTDATA FOR A UPDATED COMPANY ACCOUNT
013490**   SELECT THE COMPANY ACCOUNT WE JUST UPDATED FOR REPORT
063700**   INFORMATION
013510*********************************************************
064100     MOVE WS-TOTAL-CREDITS TO DETL2-TOT-CREDITS.
024200     MOVE WS-TOTAL-DEBITS  TO DETL2-TOT-DEBITS.
013540*    USE THE CS2COACD DAL TO READ THIS INFORMATION
063500     PERFORM B940-READ-COMPANY-ACCOUNTS THRU B940-EXIT.                                      00085200
065000     IF COAC1-ROWS-FOUND > 0
064300        MOVE COAC1-BEG-BAL-AMT TO DETL2-BEG-BAL-AMT
064400        MOVE COAC1-END-BAL-AMT TO DETL2-END-BAL-AMT
065000     ELSE
065000*    DAL WILL CALL CUTSVARB FOR A NOT FOUND CONDITION
064300        MOVE 0                TO DETL2-BEG-BAL-AMT
064400        MOVE 0                TO DETL2-END-BAL-AMT
065000     END-IF.
013640
013650 B900-EXIT.
013660     EXIT.
013670
013680 B910-BUILD-PRINTDATA-INS.
013690*********************************************************
013700**   BUILD PRINTDATA FOR A INSERTED COMPANY ACCOUNT
013710*********************************************************
064100     ADD  WS-TOTAL-CREDITS TO JLOG3-TOT-CREDITS
064100          GIVING DETL2-TOT-CREDITS.
024200     ADD  WS-TOTAL-DEBITS  TO JLOG3-TOT-DEBITS
024200          GIVING DETL2-TOT-DEBITS.
064300     MOVE 0                TO DETL2-BEG-BAL-AMT.
064400     MOVE WS-DB-CR-TOTAL   TO DETL2-END-BAL-AMT.
013780
013790 B910-EXIT.
013800     EXIT.
013810
013820 B920-BUILD-WRITE-PRINTDATA.
013830*********************************************************
013840**   BUILD PRINTDATA FOR REST OF THE FIELDS
013850**   BUILD THE SORT DATA
013860**   CALL PARA TO INSERT THE PRINTDATA RECORD
013870*********************************************************
064400     MOVE SPACES TO DETL-BAL-LVL-CD.
064400     MOVE SPACES TO DETL-ACCT-NM.
064400     MOVE SPACES TO DETL-ACCT-NO.
064400     MOVE SPACES TO DETL-SUB-ACCT-NO.
061000     MOVE SPACES TO DETL-COMPANY-DESCRIPT.
061000     MOVE SPACES TO DETL-PRODUCT-DESCRIPT.
061000     MOVE SPACES TO DETL-CONT-NO.
061000     MOVE SPACES TO DETL-CAT-NO.
061000     MOVE SPACES TO DETL-PLAN-NM.
064100
013980     IF COMPANY-LEVEL
061000*       LOOK UP COMPANY REF CODE
061000        PERFORM Z600-GET-COMPANY-DESCRIPT  THRU Z600-EXIT
014010     END-IF.
014020     IF PRODUCT-LEVEL
061000*       LOOK UP PRODUCT REF CODE
061000        PERFORM Z700-GET-PRODUCT-DESCRIPT  THRU Z700-EXIT
014050     END-IF.
014060     IF CASE-LEVEL
061000*       LOOK UP SCHEME INFORMATION
074000        PERFORM Z800-GET-CASD-DATA  THRU Z800-EXIT
014090     END-IF.
024200
064400     MOVE WS-BALANCE-LEVEL   TO  DETL-BAL-LVL-CD.
064400     MOVE ACCT1-ACCT-NM      TO  DETL-ACCT-NM.
064400     MOVE ACCT1-ACCT-NO      TO  DETL-ACCT-NO.
064400     MOVE ACCT1-SUB-ACCT-NO  TO  DETL-SUB-ACCT-NO.
064400     MOVE WS-ORIG-FDP-TR-REF-NO  TO  DETL2-TR-REF-NO.
065000*
065000* BUILD SORT-DATA
064400     INITIALIZE SORT-REC.
064400     MOVE WS-BALANCE-LEVEL      TO  SORT-BAL-LVL-CD.
064400     MOVE ACCT1-ACCT-NO         TO  SORT-ACCT-NO.
064400     MOVE ACCT1-SUB-ACCT-NO     TO  SORT-SUB-ACCT-NO.
064400     MOVE DETL-COMPANY-DESCRIPT TO  SORT-COMPANY-DESCRIPT.
064400     MOVE DETL-PRODUCT-DESCRIPT TO  SORT-PRODUCT-DESCRIPT.
074500     MOVE DETL-CONT-NO          TO  SORT-CONT-NO.
074500     MOVE DETL-CAT-NO           TO  SORT-CAT-NO.
065000*
015570     PERFORM B930-INSERT-PRINTDATA  THRU  B930-EXIT.                                            00100400
014280
014290 B920-EXIT.
014300     EXIT.
014310
014320
014330
014340 B930-INSERT-PRINTDATA.
014350*********************************************************
014360*
014370*********************************************************
014380
014390     IF LEDGER-LEVEL
074000        MOVE 'LEDG' TO WS-RECTYPE
014410     END-IF.
014420     IF COMPANY-LEVEL
074000        MOVE 'COMP' TO WS-RECTYPE
014440     END-IF.
014450     IF PRODUCT-LEVEL
074000        MOVE 'PROD' TO WS-RECTYPE
014470     END-IF.
014480     IF CASE-LEVEL
074000        MOVE 'SCHE' TO WS-RECTYPE
014500     END-IF.
014510
014520*    INCREMENT COUNTER FOR PRINT LINES
014530     ADD    +1    TO    WS-SEQ-NUM.
014540*    INSERT A NEW ROW INTO PRINTDATA FOR THE DETAIL LINE
015607*    CONTINUE CODE = Y
014560     MOVE  RPRQ1-RPTREQUESTID        TO   PRDT1-RPTREQUESTID.
014570     MOVE  WS-SEQ-NUM                TO   PRDT1-SEQNUM.
014580     MOVE  DETL-REC                  TO   PRDT1-CDL.
014590     MOVE  'Y'                       TO   PRDT1-CONTINUECD.
014600     MOVE  ZEROES                    TO   PRDT1-AUDITID.
014610     MOVE WS-RECTYPE                 TO   PRDT1-RECORDTYPE.
014620     MOVE SORT-REC                   TO   PRDT1-SORTDATA.
014630
014640     CALL 'CI1PRDTD' USING RMESSAGE,
014650                           PRDT1-DATA,
014660                           RTN-OUTPUT-FIELDS.
014670
014680
014690*    INCREMENT COUNTER FOR PRINT LINES
014700     ADD    +1    TO    WS-SEQ-NUM.
014710*    INSERT A NEW ROW INTO PRINTDATA FOR THE DETAIL LINE
014720*    CONTINUE CODE = N
014730     MOVE  RPRQ1-RPTREQUESTID        TO   PRDT1-RPTREQUESTID.
014740     MOVE  WS-SEQ-NUM                TO   PRDT1-SEQNUM.
014750     MOVE  DETL-REC2                 TO   PRDT1-CDL.
014760     MOVE  'N'                       TO   PRDT1-CONTINUECD.
014770     MOVE  ZEROES                    TO   PRDT1-AUDITID.
014780     MOVE WS-RECTYPE                 TO   PRDT1-RECORDTYPE.
014790     MOVE SORT-REC                   TO   PRDT1-SORTDATA.
014800
014810     CALL 'CI1PRDTD' USING RMESSAGE,
014820                           PRDT1-DATA,
014830                           RTN-OUTPUT-FIELDS.
014840 B930-EXIT.
014850     EXIT.
014860
014870 B940-READ-COMPANY-ACCOUNTS.
014880*********************************************************
014890**   READ A SINGLE COMPANY ACCOUNT TO GET DATA FOR REPORT
014900* SELECT BY ACCT, ACCT/CO, ACCT/PROD, OR ACCT/CASE
014910*********************************************************
014920     INITIALIZE COAC1-DATA.
014930     IF COMPANY-LEVEL
014940        MOVE WS-HOLD-CO-NUM   TO  COAC1-MKEY-CO-NUM
014950     END-IF.
014960     IF PRODUCT-LEVEL
014970        MOVE WS-HOLD-PROD-NUM  TO  COAC1-MKEY-PROD-NUM
014980     END-IF.
014990     IF CASE-LEVEL
015000        MOVE WS-HOLD-CASE-KEY  TO  COAC1-CASE-KEY
015010     END-IF.
015020
015030     MOVE WS-HOLD-ACCT-KEY              TO  COAC1-ACCT-KEY.
015040     MOVE WS-END-ACCT-BAL-DT            TO  COAC1-EFF-DT.
015050
015060     CALL 'CS2COACD' USING RMESSAGE,
015070                           COAC1-DATA,
015080                           RTN-OUTPUT-FIELDS.
015090
015100 B940-EXIT.
015110     EXIT.
015120
015130 Z000-BATCH-REPORTING.
015140**********************************************************
015150* SET UP FOR BATCH REPORTING
015160**********************************************************
015170     INITIALIZE SCAT1-DATA.
015180     MOVE FDP-SCAT-KEY  TO  SCAT1-SCAT-KEY.
015190     CALL 'CS2SCATD' USING RMESSAGE,
015200                           SCAT1-DATA,
015210                           RTN-OUTPUT-FIELDS.
015220
015230     IF RTN-NO-ERROR
015240        MOVE SCAT1-BJLG-KEY  TO  WS-BJLG-KEY
015250        INITIALIZE SCAT1-DATA
015260        MOVE FDP-SCAT-KEY  TO  SCAT1-SCAT-KEY
015270        MOVE ZEROES        TO  SCAT1-BJLG-KEY
015280
015290*       SET THE BJLG-KEY TO NULL FOR REPORTING
015300        CALL 'CU3SCATD'  USING RMESSAGE,
015310                               SCAT1-DATA,
015320                               RTN-OUTPUT-FIELDS
015330
015340     END-IF.
015350
015360 Z000-EXIT.
015370     EXIT.
015380
015390 Z100-INSERT-SCHED-ACTIVITY.
015400**********************************************************
015410* INSERT TO SCHED_ACTIVITY
015420**********************************************************
015430     CALL 'CS1TREFD'  USING RMESSAGE,
015440                            RTN-OUTPUT-FIELDS.
015450     IF RTN-NO-ERROR
015460        MOVE WS-BJLG-KEY            TO SCAT1-BJLG-KEY
015470        MOVE FDP-PRDF-KEY           TO SCAT1-PRDF-KEY
015480        MOVE FDP-TR-REF-NO          TO SCAT1-TR-REF-NO
015490        MOVE ZEROES                 TO SCAT1-CASE-KEY
015500        MOVE FDP-CYC-DT             TO SCAT1-CYC-DT
015510                                       SCAT1-BOOK-DT
015520                                       SCAT1-EFF-DT
015530                                       SCAT1-DUE-DT
015540                                       SCAT1-TX-DT
015550        MOVE '0'                    TO SCAT1-STAT-CD
015560        MOVE ZEROES                 TO SCAT1-CSRL-KEY
015570                                       SCAT1-SCAT-KEY
015580                                       SCAT1-CASE-MBR-KEY
015590                                       SCAT1-TSCH-KEY
015600                                       SCAT1-BUNDLE-ID            FDM1332
015610        MOVE SPACES                 TO SCAT1-PROC-DT
015620                                       SCAT1-UNALLOC-ACCT-CD
015630                                       SCAT1-BEN-CAT-NO           FDM1511
015640
015650*       INSERT INTO SCHED_ACTIVITY
015660        CALL 'CI1SCATD' USING RMESSAGE,
015670                              SCAT1-DATA,
015680                              RTN-OUTPUT-FIELDS
015690
015700        IF RTN-NO-ERROR
015710           MOVE SCAT1-SCAT-KEY  TO  WS-SCAT-KEY
015720           MOVE SCAT1-SCAT-KEY  TO  FDP-SCAT-KEY
015730        END-IF
015740     END-IF.
015750
015760 Z100-EXIT.
015770     EXIT.
015780
015790 Z200-UPDATE-SCHED-ACTIVITY.
015800**********************************************************
015810* UPDATE SCHED-ACTIVITY
015820**********************************************************
015830     INITIALIZE SCAT1-DATA.
015840     IF RTN-NO-ERROR
015850*       PROCESSED
015860        MOVE '2'  TO  SCAT1-STAT-CD
015870     ELSE
015880*       ERRORED
015890        MOVE '1'  TO  SCAT1-STAT-CD
015900        IF STARTUP-PROCESSING
015910           DISPLAY 'ERROR STARTING ACCOUNT BALANCE PROGRAM'
015920        ELSE
015930           DISPLAY 'ERROR PROCESSING ACCOUNT BALANCE '
015940                   ' WS-HOLD-ACCT-KEY = ' WS-HOLD-ACCT-KEY
015950           MOVE 'TX'            TO RTN-ERR-PREFIX
015960           MOVE 1005            TO RTN-ERROR-CODE
015970           MOVE 'CACBLTUB'      TO RTN-PROG-NAME
015980           MOVE 'Z200'          TO RTN-PARA-NAME
015990
016000           MOVE WS-HOLD-ACCT-NO TO WS-ERR-ACCT-NO
016010           MOVE 1               TO RTN-VAR-SEQ-NO (1)
016020           MOVE WS-ERR-MSG2     TO RTN-VAR-TEXT (1)
016030
016040           MOVE WS-HOLD-SUB-ACCT-NO TO WS-ERR-SUB-ACCT-NO
016050           MOVE 2               TO RTN-VAR-SEQ-NO (2)
016060           MOVE WS-ERR-MSG3     TO RTN-VAR-TEXT (2)
016070
016080           MOVE 2               TO RTN-VAR-DETL-CNT
016090           CALL 'CUTSVARB' USING RMESSAGE,
016100                        RTN-OUTPUT-FIELDS
016110        END-IF
016120     END-IF.
016130
016140     MOVE WS-SCAT-KEY  TO  SCAT1-SCAT-KEY.
016150     CALL 'CU1SCATD'  USING RMESSAGE,
016160                            SCAT1-DATA,
016170                            RTN-OUTPUT-FIELDS.
016180 Z200-EXIT.
016190     EXIT.
016200
016210 Z300-ROLLBACK-COMPANY-ACCOUNTS.
016220*********************************************************
016230* DELETE ALL COMPANY ACCOUNTS THAT WERE ROLLED FORWARD
016240*********************************************************
016250     INITIALIZE COAC1-DATA.
016260     MOVE WS-ORIG-FDP-TR-REF-NO TO COAC1-TR-REF-NO.
016270
016280     CALL 'CD2COACD' USING RMESSAGE,
016290                           COAC1-DATA,
016300                           RTN-OUTPUT-FIELDS.
016310
016320 Z300-EXIT.
016330     EXIT.
016340
016350 Z320-DELETE-PRINTDATA.
016360*********************************************************
016370*
016380*********************************************************
016390     INITIALIZE PRDT1-DATA.
016400     MOVE RPRQ1-RPTREQUESTID    TO  PRDT1-RPTREQUESTID.
016410
016420     CALL 'CD1PRDTD' USING RMESSAGE,
016430                           PRDT1-DATA,
016440                           RTN-OUTPUT-FIELDS.
016450
016460 Z320-EXIT.
016470     EXIT.
016480
074000 Z340-DELETE-REPORT-REQUEST.
016500*********************************************************
016510*
016520*********************************************************
073900*    RPRQ1-RPTREQUESTID ALREADY POPULATED
016540     CALL 'CD1RPRQD' USING RMESSAGE,
016550                           RPRQ1-DATA,
016560                           RTN-OUTPUT-FIELDS.
016570
016580 Z340-EXIT.
016590     EXIT.
016600
016610
016620 Z400-UPDATE-SCHED-ACT-STATUS.
016630*********************************************************
016640* SET ALL PROCESSED SCHED ACTIVITY RECORDS TO UNPROCESSED WHERE
016650* BJLG-KEY = BJLG-KEY
016660*********************************************************
016670     INITIALIZE SCAT1-DATA.
016680     MOVE WS-BJLG-KEY           TO SCAT1-BJLG-KEY.
016690
016700     CALL 'CU4SCATD' USING RMESSAGE,
016710                           SCAT1-DATA,
016720                           RTN-OUTPUT-FIELDS.
016730
016740 Z400-EXIT.
016750     EXIT.
016760
016770 Z600-GET-COMPANY-DESCRIPT.
016780*********************************************************
016790*
016800*********************************************************
016810     INITIALIZE RFCD1-DATA.
016820*    CONVERT PIC S9 TO PIC Z9
016830     MOVE WS-HOLD-CO-NUM        TO WS-REF-NUM.
016840     MOVE WS-REF-NUM            TO RFCD1-REF-CD.
016850     MOVE 'TMAP MKEY CO NUM'    TO RFCD1-DOMAIN-NAME.
016860
016870     CALL 'CS1RFCDD' USING RMESSAGE,
016880                           RFCD1-DATA,
016890                           RTN-OUTPUT-FIELDS.
016900     IF RTN-NO-ERROR
016910        MOVE RFCD1-DESCRIPT        TO DETL-COMPANY-DESCRIPT
016920     ELSE
016930        MOVE SPACES                TO DETL-COMPANY-DESCRIPT
016940     END-IF.
016950
016960 Z600-EXIT.
016970     EXIT.
016980
016990 Z700-GET-PRODUCT-DESCRIPT.
017000*********************************************************
017010*
017020*********************************************************
017030     INITIALIZE RFCD1-DATA.
017040*    CONVERT PIC S9 TO PIC Z9
017050     MOVE WS-HOLD-PROD-NUM        TO WS-REF-NUM.
017060     MOVE WS-REF-NUM              TO RFCD1-REF-CD.
017070     MOVE 'TMAP MKEY PROD NUM'    TO RFCD1-DOMAIN-NAME.
017080
017090     CALL 'CS1RFCDD' USING RMESSAGE,
017100                           RFCD1-DATA,
017110                           RTN-OUTPUT-FIELDS.
017120     IF RTN-NO-ERROR
017130        MOVE RFCD1-DESCRIPT        TO DETL-PRODUCT-DESCRIPT
017140     ELSE
017150        MOVE SPACES                TO DETL-PRODUCT-DESCRIPT
017160     END-IF.
017170
017180 Z700-EXIT.
017190     EXIT.
017200
017210 Z800-GET-CASD-DATA.
017220*********************************************************
017230*
017240*********************************************************
017250     INITIALIZE CASD3-DATA.
017260     MOVE WS-HOLD-CASE-KEY        TO CASD3-CASE-KEY.
017270
017280     CALL 'CS5CASDD' USING RMESSAGE,
017290                           CASD3-DATA,
017300                           RTN-OUTPUT-FIELDS.
017310     IF RTN-NO-ERROR
017320        MOVE CASD3-CONT-NO         TO DETL-CONT-NO
017330        MOVE CASD3-CAT-NO          TO DETL-CAT-NO
017340        MOVE CASD3-PLAN-NM         TO DETL-PLAN-NM
017350     ELSE
017360        MOVE SPACES                TO DETL-CONT-NO
017370        MOVE SPACES                TO DETL-CAT-NO
017380        MOVE SPACES                TO DETL-PLAN-NM
017390     END-IF.
017400
017410 Z800-EXIT.
017420     EXIT.
017430
017440
017450**
017460**   E N D     O F    P R O G R A M
017470**
017480**
017490
