000010*Property of FIS©. or its affiliates
000020*all rights reserved. FIS Confidential
000030 IDENTIFICATION DIVISION.
000040 PROGRAM-ID.           CACCOINB.
000050 AUTHOR.               CATHY CHAPMAN.
000060 DATE-WRITTEN.         MARCH 2006.
000070****************************************************************
000080*   $Modtime::   10 Oct 2018 10:15:16                          $
000090*   $Log::   P:/pvcsproj/DONOTUSE.prj/source/CACCOINB.COV      $
000100*
000110*   Rev 1.0.1.4   10 Oct 2018 11:45:26   alex
000120*FDM4167
000130*fis copyright
000140*
000150*   Rev 1.0.1.3   18 Apr 2006 10:32:24   Cathyc
000160*FLT0145
000170*Use mixed case in msg text, to fit in batch logs view
000180*
000190*   Rev 1.0.1.2   13 Apr 2006 10:19:08   Cathyc
000200*FLT0145
000210*ADD BRNC-KEY, TMAP-KEY; Check for sharing more than 100%
000220*
000230*   Rev 1.0.1.1   07 Apr 2006 11:28:44   Cathyc
000240*FLT0145
000250*Coinsurance, also changed JLOG insert DAL to Block Insert
000260*
000270*
000280****************************************************************
000290*  PROCESS COINSURANCE ACCOUNTING PROGRAM
000300****************************************************************
000310*
000320*  THIS PROGRAM WILL SELECT JOURNAL_LOG ROWS THAT HAVE NOT BEEN
000330*  SHARED TO COINSURERS, AND DO THE ACCOUNTING PROCESS TO SHARE
000340*  THE AMTS.  FOR EACH JLOG FOUND, IT WILL CREATE AN OFFSET
000350*  AND A COINSURER JLOG FOR THE SHARED AMT.
000360*
000370****************************************************************
000380*                     CHANGES LOG
000390****************************************************************
000400*   DATE     PRG   PROJ#      DESCRIPTION
000410* ---------  --- ---------  ------------------------------------
000420* 04-APR-06  CFC FLT0145    INITIAL CHECKIN
000430*
000440****************************************************************
000450 ENVIRONMENT DIVISION.
000460 CONFIGURATION SECTION.
000470
000480 DATA DIVISION.
000490 WORKING-STORAGE SECTION.
000500
000510 01  WS-MISC-VARIABLES.
000520     05  WS-COMPILED               PIC X(20).
000530     05  WS-PROG-NAME              PIC X(08) VALUE 'CACCOINB'.
000540
000550 01  WS-LOCAL-DATA.
000560     05  IJLOG                     PIC S9(04) COMP.
000570     05  IVJLG                     PIC S9(04) COMP.
000580     05  WS-COMMIT-LIMIT           PIC S9(05) COMP-3.
000590     05  WS-COMMIT-CT              PIC S9(05) COMP-3.
000600     05  WS-BJLC-01-CT             PIC S9(05) COMP-3.
000610     05  WS-BJLC-01-KEY            PIC S9(15) COMP-3.
000620     05  WS-TR-REF-NO              PIC X(12).
000630     05  WS-GRP-TR-REF-NO          PIC X(12).
000640     05  WS-SAVE-JLOG-KEY          PIC S9(15) COMP-3.
000650     05  WS-SAVE-MAX-VJLG1         PIC S9(05) COMP-3.
000660     05  WS-SRC-SUM                PIC S9(13)V99 COMP-3.
000670     05  WS-JRNL-AMT-CHK           PIC S9(13)V99 COMP-3.
000680
000690 01  WS-MESSAGES.
000700     05  WS-MSG-1.
000710         10  FILLER                PIC X(20)
000720                                   VALUE 'Coinsure Agreement: '.
000730         10  WS-CIAG-DESCRIPT      PIC X(20).
000740
000750     05  WS-MSG-2.
000760         10  FILLER                PIC X(13)
000770                                          VALUE 'Source-Acct: '.
000780         10  WS-SRC-ACCT-NO        PIC X(20).
000790         10  WS-SRC-ACCT-N  REDEFINES
000800             WS-SRC-ACCT-NO        PIC ZZZZZZZZZZZZZZ9.
000810         10  FILLER                PIC X(14)
000820                                         VALUE ' Target-Acct: '.
000830         10  WS-TRGT-ACCT-NO       PIC X(20).
000840         10  WS-TRGT-ACCT-N REDEFINES
000850             WS-TRGT-ACCT-NO       PIC ZZZZZZZZZZZZZZ9.
000860
000870     05  WS-MSG-3.
000880         10  FILLER                PIC X(14)
000890                                         VALUE 'Src orig amt: '.
000900         10  WS-SRC-JRNL-AMT       PIC ZZZZZZZZZZ9.99.
000910         10  FILLER                PIC X(13)
000920                                         VALUE ' reduced by: '.
000930         10  WS-MSG-JRNL-AMT-CHK   PIC ZZZZ9.99.
000940
000950*    JOURNAL_LOGS - FOR UPDATE OF SOURCE JLOG
000960     COPY CL1JLOGD.
000970*    JOURNAL_LOGS - FOR INSERT
000980     COPY CL7JLOGD.
000990*    JOIN OF COINSR TABLES AND JOURNAL_LOGS
001000     COPY CL1VJLGD.
001010*    BAT_JB_LOG_CTS
001020     COPY CL1BJLCD.
001030*    USER_DEF_DATA
001040     COPY CL1UDDTD.
001050*    LEDGER_ACCOUNTS (FOR SYS VARS)
001060     COPY CL1ACCTD.
001070
001080**  REVISION AREA **
001090 01  WS-REVISION           PIC X(35)  VALUE
001100     ' $Revision::   1.0.1.4        $'.
001110
001120 LINKAGE SECTION.
001130     COPY CLKFDPNC.
001140     COPY CL1CASDD.
001150     COPY CLKERRBC.
001160
001170*********************************************************
001180**   P R O C E D U R E    D I V I S I O N              **
001190*********************************************************
001200 PROCEDURE DIVISION USING RMESSAGE,
001210                          CASD1-DATA,
001220                          RTN-OUTPUT-FIELDS.
001230
001240     IF FDP-DEBUG-RUNSTAT > '00'
001250        ADD +1  TO RTN-DIMT-CNT
001260        MOVE WHEN-COMPILED TO
001270             RTN-DIMT-COMPILE-DATE (RTN-DIMT-CNT)
001280        MOVE WS-REVISION (16:10) TO
001290             RTN-DIMT-REVISION (RTN-DIMT-CNT)
001300        MOVE WS-PROG-NAME TO
001310           RTN-DIMT-PROGRAM-NAME (RTN-DIMT-CNT)
001320        CALL 'CUTDIMTD' USING RMESSAGE,
001330                              RTN-OUTPUT-FIELDS
001340     END-IF.
001350
001360******************************************************************
001370**                MAIN ROUTINE                                  **
001380******************************************************************
001390
001400     PERFORM A000-INITIAL
001410        THRU A000-EXIT.
001420
001430* Select and process Coinsurer share of JLOG amts
001440     IF RTN-NO-ERROR
001450        PERFORM B100-MAIN-PROCESS
001460           THRU B100-EXIT
001470     END-IF.
001480
001490* Do the final commit and update the batch job log count
001500     IF RTN-NO-ERROR AND WS-COMMIT-CT > ZEROS
001510        PERFORM Z010-COMMIT-WORK
001520           THRU Z010-EXIT
001530     END-IF.
001540
001550* Reset the TR-REF-NO to original value
001560     MOVE WS-GRP-TR-REF-NO            TO FDP-TR-REF-NO.
001570
001580     GOBACK.
001590
001600
001610 A000-INITIAL.
001620
001630     INITIALIZE JLOG1-DATA.
001640     MOVE 0 TO JLOG7-ROWS-FOUND.
001650
001660     MOVE VJLG1-MAX-ROWS           TO WS-SAVE-MAX-VJLG1.
001670     INITIALIZE VJLG1-DATA.
001680     MOVE WS-SAVE-MAX-VJLG1        TO VJLG1-MAX-ROWS.
001690
001700     MOVE 0                        TO WS-COMMIT-LIMIT
001710                                      WS-COMMIT-CT
001720                                      WS-BJLC-01-CT
001730                                      WS-SAVE-JLOG-KEY.
001740
001750     MOVE FDP-TR-REF-NO            TO WS-GRP-TR-REF-NO.
001760
001770     IF FDP-PRDF-KEY <= ZERO
001780        MOVE 'UT'                 TO RTN-ERR-PREFIX
001790        MOVE 38                   TO RTN-ERROR-CODE
001800        MOVE WS-PROG-NAME         TO RTN-PROG-NAME
001810        MOVE 'A000-INITIAL'       TO RTN-PARA-NAME
001820        CALL 'CUTSVARB' USING RMESSAGE,
001830                              RTN-OUTPUT-FIELDS
001840     END-IF.
001850*
001860     IF FDP-CYC-DT = SPACES      AND RTN-NO-ERROR
001870        MOVE 'UT'                 TO RTN-ERR-PREFIX
001880        MOVE 36                   TO RTN-ERROR-CODE
001890        MOVE WS-PROG-NAME         TO RTN-PROG-NAME
001900        MOVE 'A000-INITIAL'       TO RTN-PARA-NAME
001910        CALL 'CUTSVARB' USING RMESSAGE,
001920                              RTN-OUTPUT-FIELDS
001930     END-IF.
001940*
001950     IF FDP-BOOK-DT = SPACES     AND RTN-NO-ERROR
001960        MOVE 'UT'                 TO RTN-ERR-PREFIX
001970        MOVE 45                   TO RTN-ERROR-CODE
001980        MOVE WS-PROG-NAME         TO RTN-PROG-NAME
001990        MOVE 'A000-INITIAL'       TO RTN-PARA-NAME
002000        CALL 'CUTSVARB' USING RMESSAGE,
002010                              RTN-OUTPUT-FIELDS
002020     END-IF.
002030*
002040     IF FDP-USER = SPACES        AND RTN-NO-ERROR
002050        MOVE 'UT'                 TO RTN-ERR-PREFIX
002060        MOVE 39                   TO RTN-ERROR-CODE
002070        MOVE WS-PROG-NAME         TO RTN-PROG-NAME
002080        MOVE 'A000-INITIAL'       TO RTN-PARA-NAME
002090        CALL 'CUTSVARB' USING RMESSAGE,
002100                              RTN-OUTPUT-FIELDS
002110     END-IF.
002120*
002130     IF FDP-TR-REF-NO  = SPACES  AND RTN-NO-ERROR
002140        MOVE 'UT'                 TO RTN-ERR-PREFIX
002150        MOVE 34                   TO RTN-ERROR-CODE
002160        MOVE WS-PROG-NAME         TO RTN-PROG-NAME
002170        MOVE 'A000-INITIAL'       TO RTN-PARA-NAME
002180        CALL 'CUTSVARB' USING RMESSAGE,
002190                              RTN-OUTPUT-FIELDS
002200     END-IF.
002210*
002220     IF FDP-SCAT-KEY = SPACES    AND RTN-NO-ERROR
002230        MOVE 'UT'                 TO RTN-ERR-PREFIX
002240        MOVE 42                   TO RTN-ERROR-CODE
002250        MOVE WS-PROG-NAME         TO RTN-PROG-NAME
002260        MOVE 'A000-INITIAL'       TO RTN-PARA-NAME
002270        CALL 'CUTSVARB' USING RMESSAGE,
002280                              RTN-OUTPUT-FIELDS
002290     END-IF.
002300
002310     PERFORM A100-SETUP-COMMIT-LIMIT
002320        THRU A100-EXIT.
002330
002340     PERFORM A200-INIT-BATCH-JOB-LOG
002350        THRU A200-EXIT.
002360
002370     PERFORM A300-GET-TR-REF-NO
002380        THRU A300-EXIT.
002390
002400 A000-EXIT.
002410     EXIT.
002420
002430 A100-SETUP-COMMIT-LIMIT.
002440**   Get User Defined Data at system level
002450**   for the Coinsurer Accounting commit count
002460**   If not found, default limit to 500
002470
002480     IF RTN-NO-ERROR
002490        INITIALIZE UDDT1-DATA
002500        MOVE 1                  TO UDDT1-REF-KEY
002510        MOVE 'SYSD'             TO UDDT1-REF-TYP-CD
002520        MOVE 'COINCOMMITLIMIT'  TO UDDT1-USER-LABEL-ALIAS
002530        MOVE FDP-EFF-DT         TO UDDT1-EFF-DT
002540        CALL 'CS2VUDLD'   USING RMESSAGE,
002550                                UDDT1-DATA,
002560                                RTN-OUTPUT-FIELDS
002570        IF RTN-NO-ERROR AND UDDT1-ROWS-FOUND = 1 AND
002580           UDDT1-USER-DATA (1:5) IS NUMERIC
002590              MOVE UDDT1-USER-DATA (1:5) TO WS-COMMIT-LIMIT
002600        ELSE
002610           MOVE 500         TO WS-COMMIT-LIMIT
002620**         Issue an informational message that the default is in use
002630           MOVE 'IF'                TO RTN-ERR-PREFIX
002640           MOVE +11                 TO RTN-ERROR-CODE
002650           MOVE 'A100-SETUP-COMMIT' TO RTN-PARA-NAME
002660           MOVE WS-PROG-NAME        TO RTN-PROG-NAME
002670           MOVE 1                   TO RTN-VAR-SEQ-NO (1)
002680           MOVE 'Default commit limit of 500 will be used'
002690                                    TO RTN-VAR-TEXT (1)
002700           MOVE 1                   TO RTN-VAR-DETL-CNT
002710           CALL 'CUTSVARB'       USING RMESSAGE
002720                                    RTN-OUTPUT-FIELDS
002730        END-IF.
002740
002750 A100-EXIT.
002760     EXIT.
002770
002780
002790 A200-INIT-BATCH-JOB-LOG.
002800
002810**   Initialize the batch job log counts
002820**   01 =  processed
002830
002840     IF RTN-NO-ERROR
002850        INITIALIZE BJLC1-DATA
002860        MOVE '01'               TO BJLC1-TYP-CD
002870        MOVE FDP-BJLG-KEY       TO BJLC1-BJLG-KEY
002880        MOVE ZEROS              TO BJLC1-TYP-CT
002890        CALL 'CI1BJLCD' USING   RMESSAGE,
002900                                BJLC1-DATA,
002910                                RTN-OUTPUT-FIELDS
002920        IF RTN-NO-ERROR
002930           MOVE BJLC1-BJLC-KEY  TO WS-BJLC-01-KEY
002940        END-IF
002950     END-IF.
002960
002970** any error is hard, so do not handle BJLC error count
002980
002990 A200-EXIT.
003000     EXIT.
003010
003020 A300-GET-TR-REF-NO.
003030*  Generate a TR_REF_NO for this process
003040
003050     CALL 'CS1TREFD'               USING RMESSAGE,
003060                                         RTN-OUTPUT-FIELDS.
003070     IF RTN-NO-ERROR
003080        MOVE FDP-TR-REF-NO             TO WS-TR-REF-NO
003090     END-IF.
003100
003110 A300-EXIT.
003120     EXIT.
003130
003140
003150 B100-MAIN-PROCESS.
003160* Select JLOG rows that have not been processed
003170* and that match Coinsurance criteria
003180
003190     PERFORM UNTIL VJLG1-END-FLAG = 'Y' OR RTN-ERROR
003200
003210        CALL 'CF1VJLGD' USING RMESSAGE,
003220                              VJLG1-DATA,
003230                              RTN-OUTPUT-FIELDS
003240
003250        PERFORM VARYING IVJLG FROM 1 BY 1
003260           UNTIL IVJLG > VJLG1-ROWS-FOUND OR RTN-ERROR
003270
003280           IF VJLG1-JLOG-KEY (IVJLG) > WS-SAVE-JLOG-KEY
003290** At change of Source JLOG, update Source JLOG
003300              IF RTN-NO-ERROR
003310                 PERFORM B200-UPDATE-SRC-JLOG
003320                    THRU B200-EXIT
003330              END-IF
003340              MOVE VJLG1-JLOG-KEY (IVJLG) TO WS-SAVE-JLOG-KEY
003350           END-IF
003360
003370           IF RTN-NO-ERROR
003380              PERFORM B300-FORMAT-OFFSET-JLOG
003390                 THRU B300-EXIT
003400           END-IF
003410
003420           IF RTN-NO-ERROR
003430              PERFORM B400-FORMAT-COINSR-JLOG
003440                 THRU B400-EXIT
003450           END-IF
003460
003470           IF RTN-NO-ERROR
003480              ADD 1                TO WS-BJLC-01-CT
003490          END-IF
003500
003510        END-PERFORM
003520     END-PERFORM.
003530
003540* Do the final insert block
003550     IF RTN-NO-ERROR AND JLOG7-ROWS-FOUND > 0
003560        PERFORM X200-INSERT-JLOG
003570           THRU X200-EXIT
003580     END-IF.
003590
003600 B100-EXIT.
003610     EXIT.
003620
003630 B200-UPDATE-SRC-JLOG.
003640** Update the Source JLOG to set the COINSR-SCAT-KEY
003650** to mark the Source JLOG as Processed
003660
003670     IF RTN-NO-ERROR AND WS-COMMIT-CT = WS-COMMIT-LIMIT
003680        PERFORM Z010-COMMIT-WORK
003690           THRU Z010-EXIT
003700     END-IF.
003710
003720** For every Source JLOG read, add 1 to the commit count
003730     ADD 1   TO WS-COMMIT-CT.
003740
003750     MOVE VJLG1-JLOG-KEY (IVJLG)  TO JLOG1-JLOG-KEY
003760     MOVE FDP-SCAT-KEY            TO JLOG1-COINSR-SCAT-KEY
003770
003780     CALL 'CU8JLOGD' USING RMESSAGE,
003790                           JLOG1-DATA,
003800                           RTN-OUTPUT-FIELDS.
003810
003820** Initialize the sums for the JLOG to be shared;
003830** these are used in the 100% check.
003840     MOVE 0 TO WS-SRC-SUM
003850               WS-JRNL-AMT-CHK.
003860
003870 B200-EXIT.
003880     EXIT.
003890
003900
003910 B300-FORMAT-OFFSET-JLOG.
003920
003930     PERFORM X100-PREP-FOR-INSERT
003940        THRU X100-EXIT.
003950
003960     IF RTN-NO-ERROR
003970** Use the Source ACCT-KEY for the offsetting JLOG
003980        MOVE VJLG1-ACCT-KEY (IVJLG) TO JLOG7-ACCT-KEY (IJLOG)
003990
004000** Flip the Debit/Credit Code value for offset JLOG
004010        EVALUATE TRUE
004020        WHEN VJLG1-DB-CR-CD (IVJLG) = 'CR'
004030             MOVE 'DB' TO JLOG7-DB-CR-CD (IJLOG)
004040        WHEN VJLG1-DB-CR-CD (IVJLG) = 'CX'
004050             MOVE 'DX' TO JLOG7-DB-CR-CD (IJLOG)
004060        WHEN VJLG1-DB-CR-CD (IVJLG) = 'DB'
004070             MOVE 'CR' TO JLOG7-DB-CR-CD (IJLOG)
004080        WHEN VJLG1-DB-CR-CD (IVJLG) = 'DX'
004090             MOVE 'CX' TO JLOG7-DB-CR-CD (IJLOG)
004100        END-EVALUATE
004110
004120** Check that this share (JLOG7-) of source JLOG (VJLG1-)
004130** does not make the shares total to be > source JRNL-AMT
004140        ADD JLOG7-JRNL-AMT (IJLOG) TO WS-SRC-SUM
004150        IF WS-SRC-SUM > VJLG1-JRNL-AMT (IVJLG)
004160           PERFORM X300-CORRECTION
004170              THRU X300-EXIT
004180        END-IF
004190     END-IF.
004200
004210 B300-EXIT.
004220     EXIT.
004230
004240
004250 B400-FORMAT-COINSR-JLOG.
004260
004270     PERFORM X100-PREP-FOR-INSERT
004280        THRU X100-EXIT.
004290
004300     IF RTN-NO-ERROR
004310** Use the Target ACCT-KEY for the Coinsurer JLOG
004320        IF VJLG1-TRGT-ACCT-KEY (IVJLG) = 0
004330           MOVE VJLG1-DFLT-TRGT-ACCT-KEY (IVJLG)
004340                                  TO JLOG7-ACCT-KEY (IJLOG)
004350        ELSE
004360           MOVE VJLG1-TRGT-ACCT-KEY (IVJLG)
004370                                  TO JLOG7-ACCT-KEY (IJLOG)
004380        END-IF
004390
004400** Keep the Source Debit-Credit Code for the Coinsurer JLOG
004410        MOVE VJLG1-DB-CR-CD (IVJLG) TO JLOG7-DB-CR-CD (IJLOG)
004420     END-IF.
004430
004440** If the coinsurance sharing uses over 100% of the source,
004450** WS-JRNL-AMT-CHK will not be 0
004460** Correct the JLOG amt in the same way as the offsetting JLOG
004470     IF WS-JRNL-AMT-CHK NOT = 0
004480        COMPUTE JLOG7-JRNL-AMT (IJLOG)
004490              = JLOG7-JRNL-AMT (IJLOG) - WS-JRNL-AMT-CHK
004500        MOVE 0                    TO WS-JRNL-AMT-CHK
004510     END-IF.
004520
004530 B400-EXIT.
004540     EXIT.
004550
004560 X100-PREP-FOR-INSERT.
004570
004580** Block Insert - check if array is filled up
004590     IF JLOG7-ROWS-FOUND = JLOG7-MAX-ROWS
004600        PERFORM X200-INSERT-JLOG
004610           THRU X200-EXIT
004620        IF RTN-ERROR
004630           GO TO X100-EXIT
004640        END-IF
004650     END-IF.
004660
004670     ADD 1                         TO JLOG7-ROWS-FOUND
004680     MOVE JLOG7-ROWS-FOUND         TO IJLOG.
004690
004700** The new JLOGS' JRNL-AMT is the source JLOG's percentage amt
004710     COMPUTE JLOG7-JRNL-AMT (IJLOG) ROUNDED =
004720               (VJLG1-COINSR-PCT (IVJLG) * .01 )
004730                     * VJLG1-JRNL-AMT (IVJLG).
004740
004750** Move fields below from Source VJLG1- to JLOG7- fields.
004760     MOVE VJLG1-ACCT-KEY (IVJLG)   TO JLOG7-ACCT-KEY      (IJLOG)
004770     MOVE VJLG1-CASE-KEY (IVJLG)   TO JLOG7-CASE-KEY      (IJLOG)
004780     MOVE VJLG1-DB-CR-CD (IVJLG)   TO JLOG7-DB-CR-CD      (IJLOG)
004790     MOVE VJLG1-EFF-DT   (IVJLG)   TO JLOG7-EFF-DT        (IJLOG)
004800     MOVE VJLG1-MKEY-ACTV-NUM (IVJLG)
004810                                   TO JLOG7-MKEY-ACTV-NUM (IJLOG)
004820     MOVE VJLG1-MKEY-CO-NUM (IVJLG)
004830                                   TO JLOG7-MKEY-CO-NUM   (IJLOG)
004840     MOVE VJLG1-MKEY-FD-NUM (IVJLG)
004850                                   TO JLOG7-MKEY-FD-NUM   (IJLOG)
004860     MOVE VJLG1-MKEY-PROD-NUM (IVJLG)
004870                                   TO JLOG7-MKEY-PROD-NUM (IJLOG)
004880     MOVE VJLG1-TR-CD    (IVJLG)   TO JLOG7-TR-CD         (IJLOG)
004890     MOVE VJLG1-TMOV-KEY (IVJLG)   TO JLOG7-TMOV-KEY      (IJLOG)
004900     MOVE VJLG1-ISO-CURR-CD(IVJLG) TO JLOG7-ISO-CURR-CD   (IJLOG)
004910     MOVE VJLG1-BRNC-KEY (IVJLG)   TO JLOG7-BRNC-KEY      (IJLOG)
004920     MOVE VJLG1-TMAP-KEY (IVJLG)   TO JLOG7-TMAP-KEY      (IJLOG)
004930
004940* Move values to the JLOG fields that are not copied from Source.
004950* NOT INTERFACED TO LEDGER **
004960     MOVE '1'                     TO JLOG7-ACCT-IF-CD     (IJLOG)
004970     MOVE FDP-BOOK-DT             TO JLOG7-BOOK-DT        (IJLOG)
004980** ENTRY NOT INTERFACED TO CHECK-WRITING SYSTEM**
004990     MOVE '0'                     TO JLOG7-CHK-IF-CD      (IJLOG)
005000     MOVE FDP-CYC-DT              TO JLOG7-CYC-DT         (IJLOG)
005010** NO BALANCE PROCESSING **
005020     MOVE '0'                     TO JLOG7-HOLD-CD        (IJLOG)
005030** JOURNAL ENTRY **
005040     MOVE '0'                     TO JLOG7-JRNL-CD        (IJLOG)
005050** NOT REVERSED **
005060     MOVE '0'                     TO JLOG7-RVSL-CD        (IJLOG)
005070** N/A **
005080     MOVE '2'                     TO JLOG7-RVSL-TMAP-CD   (IJLOG)
005090** SYSTEM-GENERATED, NOT FROM TDTL **
005100     MOVE '0'                     TO JLOG7-SRC-CD         (IJLOG)
005110** NOT INTERFACED TO LEDGER **
005120     MOVE '0'                     TO JLOG7-STAT-CD        (IJLOG)
005130** THE TR-REF-NO CREATED IN THIS PROCESS **
005140     MOVE WS-TR-REF-NO            TO JLOG7-TR-REF-NO      (IJLOG)
005150
005160     MOVE 0                       TO JLOG7-CCTR-KEY       (IJLOG)
005170                                     JLOG7-REGT-KEY       (IJLOG)
005180                                     JLOG7-RVSL-TMAP-KEY  (IJLOG)
005190                                     JLOG7-ORIG-SCAT-KEY  (IJLOG)
005200     MOVE FDP-USER                TO JLOG7-LAST-MOD-USER  (IJLOG)
005210     MOVE FDP-SCAT-KEY            TO JLOG7-SCAT-KEY       (IJLOG)
005220                                     JLOG7-COINSR-SCAT-KEY(IJLOG)
005230     MOVE VJLG1-JLOG-KEY (IVJLG)  TO JLOG7-COINSR-JLOG-KEY(IJLOG).
005240
005250 X100-EXIT.
005260     EXIT.
005270
005280
005290 X200-INSERT-JLOG.
005300
005310** Block insert
005320     CALL 'CI1JLOGD' USING RMESSAGE,
005330                           JLOG7-DATA,
005340                           RTN-OUTPUT-FIELDS.
005350
005360     MOVE 0 TO JLOG7-ROWS-FOUND.
005370
005380 X200-EXIT.
005390     EXIT.
005400
005410
005420
005430 X300-CORRECTION.
005440** Correct the coinsr JLOGs so they do not share more than 100%
005450
005460** Subtract from offset amt so that the total of offsets
005470**   is not larger than the source JRNL-AMT.
005480
005490     COMPUTE WS-JRNL-AMT-CHK
005500           = WS-SRC-SUM - VJLG1-JRNL-AMT (IVJLG).
005510     COMPUTE JLOG7-JRNL-AMT (IJLOG)
005520           = JLOG7-JRNL-AMT (IJLOG) - WS-JRNL-AMT-CHK.
005530
005540** Subtract the same amount from WS-SRC-SUM in case there are
005550** more offsets for the same source JLOG
005560     COMPUTE WS-SRC-SUM = WS-SRC-SUM - WS-JRNL-AMT-CHK.
005570
005580     MOVE VJLG1-SRC-ACCT-KEY (IVJLG)          TO ACCT1-ACCT-KEY.
005590     PERFORM X310-GET-ACCT-NO
005600        THRU X310-EXIT.
005610
005620     IF RTN-NO-ERROR
005630        MOVE ACCT1-ACCT-NO                    TO WS-SRC-ACCT-NO
005640     ELSE
005650        MOVE VJLG1-SRC-ACCT-KEY (IVJLG)       TO WS-SRC-ACCT-N
005660     END-IF.
005670
005680     IF VJLG1-TRGT-ACCT-KEY (IVJLG) > 0
005690        MOVE VJLG1-TRGT-ACCT-KEY (IVJLG)      TO ACCT1-ACCT-KEY
005700     ELSE
005710        MOVE VJLG1-DFLT-TRGT-ACCT-KEY (IVJLG) TO ACCT1-ACCT-KEY
005720     END-IF.
005730
005740     PERFORM X310-GET-ACCT-NO
005750        THRU X310-EXIT.
005760
005770     IF RTN-NO-ERROR
005780        MOVE ACCT1-ACCT-NO                    TO WS-TRGT-ACCT-NO
005790     ELSE
005800        IF VJLG1-TRGT-ACCT-KEY (IVJLG) > 0
005810           MOVE VJLG1-TRGT-ACCT-KEY (IVJLG)   TO WS-TRGT-ACCT-N
005820        ELSE
005830           MOVE VJLG1-DFLT-TRGT-ACCT-KEY (IVJLG)
005840                                              TO WS-TRGT-ACCT-N
005850        END-IF
005860     END-IF.
005870
005880** Warning: coinsurance shares have used more than 100% of AMT
005890     MOVE 'JL'                    TO RTN-ERR-PREFIX
005900     MOVE 77                      TO RTN-ERROR-CODE
005910     MOVE WS-PROG-NAME            TO RTN-PROG-NAME
005920     MOVE 'X300-CORRECTION'       TO RTN-PARA-NAME
005930     MOVE VJLG1-CIAG-DESCRIPT(IVJLG) TO WS-CIAG-DESCRIPT
005940     MOVE WS-MSG-1                TO RTN-VAR-TEXT (1)
005950     MOVE 1                       TO RTN-VAR-SEQ-NO (1)
005960     MOVE WS-MSG-2                TO RTN-VAR-TEXT (2)
005970     MOVE 2                       TO RTN-VAR-SEQ-NO (2)
005980     MOVE VJLG1-JRNL-AMT (IVJLG)  TO WS-SRC-JRNL-AMT
005990     MOVE WS-JRNL-AMT-CHK         TO WS-MSG-JRNL-AMT-CHK
006000     MOVE WS-MSG-3                TO RTN-VAR-TEXT (3)
006010     MOVE 3                       TO RTN-VAR-SEQ-NO (3)
006020     MOVE 3                       TO RTN-VAR-DETL-CNT
006030     CALL 'CUTSVARB' USING RMESSAGE,
006040                           RTN-OUTPUT-FIELDS.
006050
006060 X300-EXIT.
006070     EXIT.
006080
006090
006100 X310-GET-ACCT-NO.
006110
006120     CALL 'CS2ACCTD' USING RMESSAGE,
006130                           ACCT1-DATA,
006140                           RTN-OUTPUT-FIELDS.
006150 X310-EXIT.
006160     EXIT.
006170
006180
006190**************************************************************
006200** COMMIT ALL THE WORK DONE AND UPDATE JOB LOG COUNTS.
006210**************************************************************
006220 Z010-COMMIT-WORK.
006230
006240     IF FDP-DEBUG > 0
006250        DISPLAY WS-PROG-NAME, ' Z010-COMMIT-WORK'
006260               ' WS-COMMIT-CT: ', WS-COMMIT-CT
006270     END-IF.
006280
006290**   Update the batch job log counts before the commit
006300**   01 =  processed
006310
006320     IF RTN-NO-ERROR
006330        INITIALIZE BJLC1-DATA
006340        MOVE WS-BJLC-01-KEY     TO BJLC1-BJLC-KEY
006350        MOVE WS-BJLC-01-CT      TO BJLC1-TYP-CT
006360        CALL 'CU1BJLCD' USING   RMESSAGE,
006370                                BJLC1-DATA,
006380                                RTN-OUTPUT-FIELDS
006390     END-IF.
006400
006410**   Commit all the work done
006420
006430     IF RTN-NO-ERROR
006440        CALL 'CCOMMITD'
006450     END-IF.
006460
006470     MOVE ZEROS          TO WS-COMMIT-CT.
006480     MOVE ZEROS          TO WS-BJLC-01-CT.
006490
006500 Z010-EXIT.
006510     EXIT.
006520
