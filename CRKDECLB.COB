000010*Property of FIS©. or its affiliates
000020*all rights reserved. FIS Confidential
000030 IDENTIFICATION DIVISION.
000040 PROGRAM-ID.           CRKDECLB.
000050 AUTHOR.               PAUL TETREAULT.
000060 DATE-WRITTEN.         JAN 2006.
000070****************************************************************
000080*   $Modtime::   08 Aug 2022 14:14:28  $
000090*   $Log::   P:/pvcsproj/DONOTUSE.prj/source/CRKDECLB.COV  $
000100*
000110*   Rev 1.0.1.4   09 Aug 2022 11:19:26   paul
000120*OMM2923
000130*Restore FDP-CASE-MBR-KEY
000140*
000150*   Rev 1.0.1.3   10 Oct 2018 17:29:34   alex
000160*FDM4167
000170*fis copyright
000180*
000190*   Rev 1.0.1.2   11 Dec 2007 09:16:08   eric
000200*PFM0298
000210*Linkage variance issued in program CRKDECLB while running risk
000220*
000230*   Rev 1.0.1.1   07 Feb 2006 17:17:10   paul
000240*PFM0091
000250*Free Cover Limits
000260****************************************************************
000270*                     CHANGES LOG
000280****************************************************************
000290*
000300*   DATE     PRG   PROJ#      DESCRIPTION
000310*  --------  --- ---------  --------------------------------------
000320*  01/2006   PDT  PFM0091   INITIAL CODING.                       PFM0091
000330*  12/2007   EDM  PFM0298   REMOVE EDIT ON FDP-REF-KEY.           PFM0298
000340*  08/2022   PDT  OMM2923   SAVE AND RESTORE FDP-CASE-MBR-KEY.
000350****************************************************************
000360 ENVIRONMENT DIVISION.
000370 CONFIGURATION SECTION.
000380*
000390 DATA DIVISION.
000400 WORKING-STORAGE SECTION.
000410
000420 01 BEG-WORK-STOR.
000430    05 FILLER                             PIC X(40) VALUE
000440       'THIS IS THE BEGINNING OF WORKING STORAGE'.
000450
000460 01  WS-MISC-VARIABLES.
000470     05  WS-COMPILED                      PIC X(20).
000480     05  WS-SAVE-CASE-MBR-KEY             PIC S9(15) COMP-3.      OMM2923
000490
000500 01  WS-SUBSCRIPTS.
000510     05  BNTP                             PIC S9(5) COMP.
000520
000530*    DAL WORKING STORAGE / LINKAGE COPYBOOKS
000540
000550*    BEN_TRNCH_DETL
000560     COPY CL2BNTPD.
000570
000580**  REVISION AREA **
000590 01  WS-REVISION           PIC X(35)  VALUE
000600     ' $Revision::   1.0.1.4  $'.
000610
000620 LINKAGE SECTION.
000630     COPY CLKFDPNC.
000640     COPY CL1CASDD.
000650     COPY CLKERRBC.
000660
000670*********************************************************
000680**   P R O C E D U R E    D I V I S I O N              **
000690*********************************************************
000700
000710 PROCEDURE DIVISION USING RMESSAGE,
000720                          CASD1-DATA,
000730                          RTN-OUTPUT-FIELDS.
000740     IF FDP-DEBUG-RUNSTAT > '00'
000750        ADD +1  TO RTN-DIMT-CNT
000760        MOVE WHEN-COMPILED TO
000770             RTN-DIMT-COMPILE-DATE (RTN-DIMT-CNT)
000780        MOVE WS-REVISION (16:10) TO
000790             RTN-DIMT-REVISION (RTN-DIMT-CNT)
000800        MOVE 'CRKDECLB' TO
000810           RTN-DIMT-PROGRAM-NAME (RTN-DIMT-CNT)
000820        CALL 'CUTDIMTD' USING RMESSAGE,
000830                              RTN-OUTPUT-FIELDS
000840     END-IF.
000850
000860******************************************************************
000870**                MAIN ROUTINE                                  **
000880******************************************************************
000890
000900     PERFORM A000-INITIAL
000910        THRU A000-EXIT.
000920
000930     IF RTN-NO-ERROR
000940        PERFORM B000-PROCESS-MEMBER
000950           THRU B000-EXIT
000960     END-IF.
000970
000980     GOBACK.
000990
001000 A000-INITIAL.
001010**
001020**   EDIT INCOMING PARMS
001030**
001040     PERFORM A100-READ-PARMS
001050        THRU A100-EXIT.
001060
001070 A000-EXIT.
001080     EXIT.
001090
001100 A100-READ-PARMS.
001110
001120     IF FDP-CASE-KEY <= ZERO
001130        MOVE 'UT'            TO RTN-ERR-PREFIX
001140        MOVE 30              TO RTN-ERROR-CODE
001150        MOVE 'CRKDECLB'      TO RTN-PROG-NAME
001160        MOVE 'A100-READ'     TO RTN-PARA-NAME
001170        CALL 'CUTSVARB' USING RMESSAGE,
001180                              RTN-OUTPUT-FIELDS
001190     END-IF.
001200
001210     IF FDP-EFF-DT = SPACES AND RTN-NO-ERROR
001220        MOVE 'UT'            TO RTN-ERR-PREFIX
001230        MOVE 35              TO RTN-ERROR-CODE
001240        MOVE 'CRKDECLB'      TO RTN-PROG-NAME
001250        MOVE 'A100-READ'     TO RTN-PARA-NAME
001260        CALL 'CUTSVARB' USING RMESSAGE,
001270                              RTN-OUTPUT-FIELDS
001280     END-IF.
001290
001300     IF FDP-CYC-DT = SPACES AND RTN-NO-ERROR
001310        MOVE 'UT'            TO RTN-ERR-PREFIX
001320        MOVE 36              TO RTN-ERROR-CODE
001330        MOVE 'CRKDECLB'      TO RTN-PROG-NAME
001340        MOVE 'A100-READ'     TO RTN-PARA-NAME
001350        CALL 'CUTSVARB' USING RMESSAGE,
001360                              RTN-OUTPUT-FIELDS
001370     END-IF.
001380
001390     IF FDP-USER = SPACES AND RTN-NO-ERROR
001400        MOVE 'UT'            TO RTN-ERR-PREFIX
001410        MOVE 39              TO RTN-ERROR-CODE
001420        MOVE 'CRKDECLB'      TO RTN-PROG-NAME
001430        MOVE 'A100-READ'     TO RTN-PARA-NAME
001440        CALL 'CUTSVARB' USING RMESSAGE,
001450                              RTN-OUTPUT-FIELDS
001460     END-IF.
001470
001480 A100-EXIT.
001490     EXIT.
001500
001510 B000-PROCESS-MEMBER.
001520
001530** FETCH BENEFIT RECORDS THAT WERE UPDATED IN THE CURRENT RISK
001540** RUN AND THAT HAVE BEEN PREVIOUSLY DECLINED - UNDERWRITING
001550** STATUS WITH AN ACTION CODE EQUAL TO PROCESSED DECLINED.
001560
001570     MOVE FDP-CASE-MBR-KEY TO WS-SAVE-CASE-MBR-KEY.               OMM2923
001580
001590     INITIALIZE BNTP2-DATA.
001600
001610     PERFORM UNTIL BNTP2-END-FLAG = 'Y' OR RTN-ERROR
001620
001630       CALL 'CF4VBTDD' USING RMESSAGE,
001640                             BNTP2-DATA,
001650                             RTN-OUTPUT-FIELDS
001660        PERFORM VARYING BNTP FROM 1 BY 1
001670          UNTIL BNTP > BNTP2-ROWS-FOUND OR RTN-ERROR
001680           PERFORM B100-ISSUE-WARNING
001690              THRU B100-EXIT
001700        END-PERFORM
001710
001720     END-PERFORM.
001730
001740     MOVE WS-SAVE-CASE-MBR-KEY TO FDP-CASE-MBR-KEY.               OMM2923
001750
001760 B000-EXIT.
001770     EXIT.
001780
001790 B100-ISSUE-WARNING.
001800
001810     MOVE BNTP2-CASE-MBR-KEY (BNTP) TO FDP-CASE-MBR-KEY.
001820     MOVE 'CRKDECLB'                TO RTN-PROG-NAME.
001830     MOVE 'B100-'                   TO RTN-PARA-NAME.
001840     MOVE 'RK'                      TO RTN-ERR-PREFIX.
001850     MOVE 59                        TO RTN-ERROR-CODE.
001860     MOVE 1                         TO RTN-VAR-SEQ-NO (1).
001870     MOVE 1                         TO RTN-VAR-DETL-CNT.
001880     STRING 'Benefit - ' DELIMITED BY SIZE
001890             BNTP2-DESCRIPT (BNTP) DELIMITED BY SIZE
001900                                  INTO RTN-VAR-TEXT (1).
001910     CALL 'CUTSVARB' USING RMESSAGE,
001920                           RTN-OUTPUT-FIELDS.
001930
001940 B100-EXIT.
001950     EXIT.
